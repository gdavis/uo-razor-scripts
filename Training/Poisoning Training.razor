# Starting at 50 poisoning to GM

# NOTE:
# this is just for using potions made
# by Sully as he levels alchemy

if not varexist 'poison_target'
    overhead 'Poison what weapon?'
    setvar 'poison_target'
endif

# try to refill empty bottles with cure potions from a keg
if findtype 'keg' backpack 44
    while findtype 'keg' backpack 44 as cureKeg and counttype 'Orange Potion' backpack < 25
        if findtype 'empty bottle%s%' backpack
            dclick  cureKeg
        else
            overhead 'No empty bottles!' 38
            break
        endif
    endwhile
else
    overhead 'missing cure keg, probably gonna die!' 38
endif

if poisoned and findtype 'Orange Potion' backpack as cure
    dclick cure
endif
    
if hp < maxhp or poisoned
    if skill 'Chivalry' > 50
        gumpresponse 16
        waitfortarget 1000
        target 'self'
        
        if hp < 40
            gumpresponse 19
            waitforgump 1387930325
        endif  
    endif    

    if findtype 'clean bandage%s%' backpack 
        clearsysmsg
        dclicktype 'clean bandage%s%' backpack
        wft 1000
        target self
        wait 500
            
        if insysmsg 'You begin applying the bandages'
            settimer 'bandageSelf' 0
            wait 10250
            if hp < 100 
                overhead 'Bandage: still hurt, reapplying...'
                replay
            endif
        elseif insysmsg 'That being is not damaged'
            overhead 'Full Health' 1001
        else
            overhead 'Did it apply?!?' 38
        endif
    else 
        overhead 'No bandages!' 38
        wait 1000
        loop
    endif
endif

if findtype 'Green Potion' backpack as potion
    skill 'poisoning'
    waitfortarget
    target potion

    # use on item
    waitfortarget 1000
    target 'poison_target'
    
    wait 500
    # check if weve been poisoned
    if findbuff 'poison'
        overhead 'Poisoned!' 44
        hotkey 'Drink Cure'
    endif
    
    wait 5000
else 
    if not findtype 'keg' backpack 363 and findtype 'keg' 0x46512083 363 as newkeg
#        lift newkeg 1
#        drop backpack 0 0 0
        dclick newkeg
        wait 250
    endif

    # refill from a poison keg in inventory
    if findtype 'keg' backpack 363 as keg 
        getlabel keg kegName
        @setvar useKeg 0
        overhead kegName

        if skill 'poisoning' < 55
            overhead 'need regular poison' 920
        elseif skill 'poisoning' < 80
           overhead 'need greater poison' 920
        elseif skill 'poisoning' < 100 
            if 'deadly' in kegName
                overhead 'found deadly keg' 920
                @setvar useKeg 1
            else 
                overhead 'need deadly poison' 920
            endif  
        else 
            overhead 'need leathal poison' 920
        endif
    
        if useKeg == 1 
            overhead 'Refilling from keg...' 920
            while counttype 'empty bottle%s%' self > 0
                dclick keg
                
                getlabel keg kegName
                if 'is empty' in kegName
                    break
                endif  
            endwhile
            
            loop
        endif
    else 
        overhead 'No kegs found to refill from' 38
        wait 2500
    endif

    # check for existing potions in the chest
    if findtype 'Green Potion' 0x46512083 as potion
        overhead 'Restocking potions...' 920
        lift potion 25
        drop backpack 0 0 0
    else
        overhead 'No potions left' 38
        wait 2500
        loop
    endif
endif

# this will move empty bottles to 
# the left chest in the rental room
if counttype 'empty bottle%s%' self >= 25 and findtype 'empty bottle%s%' backpack as bottles
    overhead 'moving bottles...' 920
    lift bottles 25
    
    if findtype 'empty bottle%s%' 0x46512083 as stack
        # add emptle bottles to the existing stack
        drop stack
    else
        # add a new stack in the chest
        drop 0x4B6FA8C9 76 151 0
    endif
endif

    
loop