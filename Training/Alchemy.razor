# Training Sullivahn for GM Alchemy

# ----------------------------
# Prechecks for resources

if not varexist 'regbox'
    overhead 'Where to refill regs from?'
    setvar 'regbox'
endif

if not findtype 'empty bottle%s%' backpack
    if findtype 'empty bottle%s%' 0x4B6FA8C9 as bottles
        overhead 'Restocking bottles...'
        lift bottles 25
        drop backpack 0 0 0
    else
        overhead 'No more bottles' 38
    endif
endif

if not findtype 'mortar and pestle' backpack
    overhead 'Missing mortar and pestle!' 38
    wait 5000
    loop
endif


if counttype 'Nightshade' backpack < 10
    dclick 'regbox'
    if findtype 'Nightshade' 'regbox' as ns
        lift ns 200
        drop backpack 0 0 0 
    endif
endif

# 3981 = spider silk
if counttype 3981 backpack < 4
    overhead 'Missing spider silk!' 38
    dclick 'regbox'
    if findtype 3981 'regbox' as ss
        lift ss 200
        drop backpack 0 0 0
    endif    
    
    wait 5000
    loop
endif

# Lesser Poison
if skill 'Alchemy' < 55

# Lesser Magic Resist
elseif skill 'Alchemy' < 75

# Greater Poison
elseif skill 'Alchemy' < 95

# Deadly Poison
elseif skill 'Alchemy' < 115

# Lethal Poison
elseif skill 'Alchemy' < 120

# Lesser Magic Resist
else   
    overhead "Legendary Poisoner!"
endif


# ----------------------------
# Do the Alchemy
gumpclose 
dclicktype "mortar and pestle" backpack
waitforgump 949095101

# Lesser Poison
if skill 'Alchemy' < 55
    overhead '50 - 55: Lesser Poison'
    gumpresponse 106
    waitforgump 949095101
    gumpresponse 200
    waitforgump 949095101
  
# Poison option for training.  
#elseif skill 'Alchemy' < 75
#    overhead '55 - 75: Regular Poison'
#    gumpresponse 106
#    waitforgump 949095101
#    gumpresponse 201
#    waitforgump 949095101
    
# Lesser Magic Resist
elseif skill 'Alchemy' < 75
    overhead '55 - 75: Lesser Magic Resist'
    gumpresponse 105
    waitforgump 949095101
    gumpresponse 200
    waitforgump 949095101
    
    # chug, chug, chug! 
    # use em up for the bottles
    if counttype 'Black Potion' >= 25
        while findtype 'Black Potion' backpack as potion
            dclick potion 
        endwhile
    endif

# Greater Poison
elseif skill 'Alchemy' < 95
    overhead '75 - 95: Greater Poison'
    gumpresponse 106
    waitforgump 949095101
    gumpresponse 202
    waitforgump 949095101
    
# Deadly Poison
elseif skill 'Alchemy' < 115
    overhead '95 - 115: Deadly Poison'
    gumpresponse 106
    waitforgump 949095101
    gumpresponse 203
    waitforgump 949095101
    
# Lethal Poison
elseif skill 'Alchemy' < 120
    overhead '115 - 120: Lethal Poison'
    gumpresponse 106
    waitforgump 949095101
    gumpresponse 204
    waitforgump 949095101

# Done
else   
    overhead "Legendary Poisoner!"
endif

wait 2000

if ingump 'You fail to create a useful potion' 949095101
    overhead 'You Failed.' 38
elseif ingump 'You pour the potion' 949095101
    overhead 'Success!' 84
endif


# ---------------------------
# Move potion to the keg
clearsysmsg 

# grab keg from box if we dont have it
if not findtype 'keg' backpack
    if findtype 'keg' 0x4B6FA8C9 0 as newkeg
        overhead 'grabbing new empty keg'
        lift newkeg 1
        drop backpack 79 64 0
    else 
        overhead 'no more kegs!'
        wait 5000
        loop
    endif
endif

if findtype 'Green Potion' self as potion
    overhead 'created potion'
endif    

if findtype 'Green Potion' self as potion and findtype 'keg' self as keg
    overhead 'moving potion to keg'    
    lift potion 25
    drop keg 0 0 0
endif

wait 250

if insysmsg 'the keg will not hold' and findtype 'keg' self as keg
    overhead 'keg full!'
    overhead 'moving to chest...'
    lift keg 1
    drop 0x4B6FA8C9 93 126 0
endif

# ----------------------------
# Move potion to the chest
#while counttype 'Green Potion' self >= 24 and findtype 'Green Potion' backpack as potion 
#    overhead 'moving potions...'    
#    lift potion 25
#    drop 0x4B6FA8C9 76 151 0
#endwhile

loop