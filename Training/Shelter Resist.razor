# cast on yourself at shelter
while diffhits >= 30 and not dead
    clearsysmsg 
    cast 'Greater Heal'
    overhead '[G-Heal Self]' 24
    wait 250
    
    if insysmsg 'You are already casting a spell'
        overhead '[Cancel Last Spell]' 38
        waitfortarget 
        hotkey 'Cancel Current Target' 38
        loop
    endif

    if insysmsg 'You have not yet recovered'
        overhead '[Not Recovered]' 38
        wait 100
        loop
    endif
    
    waitfortarget 
    target self
    warmode 'on'
    warmode 'off'
    wait 500
    break
endwhile

@setvar! minMana 0
@setvar! spellID 0

if skill 'resisting spells' < 60
    @setvar! minMana 15
    @setvar! spellID 1
elseif skill 'resisting spells' < 70
    @setvar! minMana 25
    @setvar! spellID 2
elseif skill 'resisting spells' < 80
    @setvar! minMana 30
    @setvar! spellID 3
elseif skill 'resisting spells' < 100
    @setvar! minMana 40
    @setvar! spellID 4
else
    overhead 'unfinished' 38
    wait 4000
    loop
endif

if minMana > 0 and mana < minMana
    skill 'Meditation'
    wait 500

    if findbuff 'actively meditating'
        while mana < maxmana
            overhead 'Meditating...'
            wait 3500
        endwhile
        replay
    else
        overhead 'Retrying meditation...'
        wait 5500
        replay
    endif
endif 

if spellID = 1 
    overhead '50-60'
    hotkey 'Lightning'
elseif spellID = 2
    overhead '60-70'
    hotkey 'Mind Blast'
elseif spellID = 3   
    overhead '70-80'
    hotkey 'Energy Bolt'
elseif spellID = 4 
    overhead '80-100'
    hotkey 'Flamestrike'
else 
    overhead 'Unfinished' 38
    wait 4000
    loop
endif

waitfortarget
target 'self'
wait 1000
loop