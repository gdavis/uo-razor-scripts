# Inspired by NBK bot.
# Uses inscription character to cast 
# long buffs on players.
@setvar! buffCooldown 300000
@setvar! speakCooldown 30000

# set to 1 to callback to check for distillations
# on a given delay
@setvar! distillBot 1
@setvar! runDistillScriptDelay 10000

if not timerexists distillScriptTimer
    createtimer distillScriptTimer
endif

if distillBot = 1 and timer distillScriptTimer > runDistillScriptDelay
    settimer distillScriptTimer 0
    script 'DistillGuildhall'
endif

# ignore list
# 0x5646F aka 353391 - Dobbey
# 2910331 - Alchemist Trav
removelist ignoreList
if not listexists ignoreList
    createlist ignoreList
    pushlist ignoreList 353391 back
    pushlist ignoreList 2910331 back
    pushlist ignoreList 2460069 back
    pushlist ignoreList 1492384 back
    pushlist ignoreList 1248535 back
endif

# scratchpad for saving player IDs
if not varexist scratchpad
    overhead 'Select a marked rune that can be used as a scratchpad:'
    setvar scratchpad
endif

# timer for announcing service
if not timerexists speakTimer
    createtimer speakTimer
endif

# timeout for someone moving out of range
@setvar! rangeTimeout 6000
if not timerexists rangeTimer
    createtimer rangeTimer
endif

if mana < 30
    say "Gah, I cannot concentrate! Shut up and let me think a moment..."
    wait 5000
    loop
endif

# check for reagents
@setvar! needRegsDelay 15000

while findtype 3972|3974|3980|3973 ground any any 2 as regs
    say 'Oh ho! Just what I needed. Thanks pal.'
    lift regs 60000
    
    if findtype 'reagent satchel' self as satchel
        drop satchel -1 -1 -1
    else 
        drop backpack -1 -1 -1
    endif
    wait 500
endwhile

if counttype 3972 self == 0
    say "I am out of garlic! Please drop some on the ground for me."
    wait needRegsDelay
    replay
elseif counttype 3974 self == 0
    say "I am out of mandrake root! Please drop some on the ground for me."
    wait needRegsDelay
    replay
elseif counttype 3980 self == 0
    say "I am out of sulphurous ash! Please drop some on the ground for me."
    wait needRegsDelay
    replay
elseif counttype 3973 self == 0
    say "I am out of ginseng! Please drop some on the ground for me."
    wait needRegsDelay
    replay
endif


# find a target
#overhead ' '
clearsysmsg 

if targetexists 
    hotkey "Clear Target Queue"
    hotkey "Cancel Current Target"
endif

hotkey "Next Friendly Player Target"

if insysmsg 'New target set' 
    @setvar! player lasttarget
    sysmsg 'ID: {{player}}' 472

    if not varexist lastPlayerID
        @setvar! lastPlayerID player
    elseif lastPlayerID = player
        sysmsg 'Waiting for new target...' 908
        wait 2500
    else 
        @setvar! lastPlayerID player
    endif
    

    if inlist ignoreList player
        sysmsg 'Ignoring player.' 138
        loop
    endif
    
    if findtype 3999 backpack as scissors
        settimer rangeTimer 0
        dclick scissors
        wft 6000
        target player 
        wait 666
        
        while 1 = 1
            if timer rangeTimer > rangeTimeout
                hotkey 'Cancel Current Target'
                overhead 'Player moved away?' 44
                break
                
            elseif insysmsg 'cannot be used on that'
                #overhead 'in range'
                
                # store the player ID as the name of the rune,
                # so we can then get that player ID and use for
                # storing a timer for that player.
                dclick scratchpad
                waitforprompt 1036 1000
                promptresponse player
                getlabel scratchpad playerID    
                sysmsg 'BUFFBOT: found target {{playerID}}' 48
                
                # check if player is on cooldown
                if timerexists playerID
                    if timer playerID > buffCooldown
                        sysmsg 'BUFFBOT: {{playerID}} can be buffed again.' 84
                        settimer playerID 0
                    else
                        sysmsg 'BUFFBOT: {{playerID}} has been buffed too recently.' 38
                        break
                    endif
                endif
                
                if insysmsg 'The world is saving'
                    clearsysmsg 
                    wait 6000
                endif
                
                emote 'opens spellbook'
                cast 'Bless'
                wft 6000
                
                target player
                wait 1500
                
                if insysmsg 'existing effect of equal or greater'
                    say "You still have an old buff on, come back later."
                    wait 500
                    emote 'slams spellbook closed'
                
                elseif targetexists 
                    say 'Did you just walk away from me? Hrmph.'
                else             
                    cast 'Arch Protection'
                    wft 6000
                    
                    target player
                    wait 1000
                    
                    say 'Next!'
                    
                    createtimer playerID
                    settimer playerID 0
                endif

                break
                
            elseif insysmsg 'too far away.' or insysmsg 'target is out of range' or insysmsg 'Target cannot be seen'
                #overhead 'not in range'
            
                @setvar! shouldSpeak 0
                # the larger the random number,
                # the less frequent the speech
                random 600
                # needs to be the length of
                # the number of digits from
                # the random
                if insysmsg 'Random: 111'
                    @setvar! shouldSpeak 1
                    clearsysmsg 
                endif
            
                # announce when new person enters, on a cooldown
                #if timer speakTimer > speakCooldow
                if shouldSpeak = 1
                    settimer speakTimer 0

                    # NOTE: now that this script also goes
                    # back to the distill script, we cannot
                    # keep timers going that are longer than 
                    # the delay to go to the next script.
                    # therefore, to not spam, 
                    # use a random value higher
                    # than the messages we have
                    # to effectively be silent sometimes.
                    random 5
                    
                    if insysmsg 'Random: 1'
                        say "I provide free buffs if you stand next to me."
                    elseif insysmsg 'Random: 2'
                        say "Nice day for a blessin', ain't it?"
                    elseif insysmsg 'Random: 3'
                        emote 'whistles'
                    elseif insysmsg 'Random: 4'
                        say "Headed out? Grab a blessing before you go."
                    elseif insysmsg 'Random: 5'
                        say "I can also make distills! Place an extract in box next to me."
                    else 
                        # do nothing. 
                        overhead 'No message defined.' 38
                    endif

                    wait 2000
                endif

                break
            else
                wait 100
            endif
        endwhile
    else
        overhead 'Scissors are required for a range check' 38
        wait 2000
    endif
else
    overhead 'Target: None' 908
    wait 250
endif

loop