# Inspired by NBK bot.
# Uses inscription character to cast 
# long buffs on players.
@setvar! buffCooldown 60000
@setvar! speakCooldown 120000

overhead ' '
clearsysmsg 
hotkey "Clear Target Queue"
hotkey "Cancel Current Target"
hotkey "Next Friendly Player Target"
@setvar! player lasttarget

if not varexist scratchpad
    overhead 'Select a marked rune that can be used as a scratchpad:'
    setvar scratchpad
endif

if not timerexists speakTimer
    createtimer speakTimer
endif

@setvar! rangeTimeout 6000
if not timerexists rangeTimer
    createtimer rangeTimer
endif

if timer speakTimer > speakCooldown
    settimer speakTimer 0
    say 'Hello NEW! members! I am training to become a reliable buff provider.'
    wait 4000
    say 'Come stand next to me to receive my blessing.'
    wait 2000
    say 'If anything goes wrong blame my maker.'
    wait 2000
endif

if mana < 30
    say 'I am feeling a bit exhausted. Let me rest a moment...'
    wait 5000
    loop
endif

if insysmsg 'New target set' 
    
    if findtype 'scissors' backpack as scissors
        settimer rangeTimer 0
        dclick scissors
        wft 6000
        target player 
        wait 650
        
        while 1 = 1
            if timer rangeTimer > rangeTimeout
                hotkey 'Cancel Current Target'
                overhead 'Player moved away?' 38
                break
                
            elseif insysmsg 'cannot be used on that'
                #overhead 'in range'
            
                if timerexists playerID
                    if timer playerID > buffCooldown
                        sysmsg 'BUFFBOT: {{playerID}} can be buffed again.' 38
                        settimer playerID 0
                    else
                        sysmsg 'BUFFBOT: {{playerID}} buffed too recently.' 908
                        break
                    endif
                endif
                
                # store the player ID as the name of the rune,
                # so we can then get that player ID and use for
                # storing a timer for that player.
                dclick scratchpad
                waitforprompt 1036 1000
                promptresponse player
                getlabel scratchpad playerID    
                sysmsg 'BUFFBOT: found target {{playerID}}'
                
                emote 'lifts head toward you'
                wait 1500
                
                say 'Hello friend. Hold still while I bring the blessings of Sosaria to you...'
                wait 1000
                
                emote 'opens spellbook'
                wait 100
                
                cast 'Bless'
                wft 6000
                target player
                wait 1500
                
                if insysmsg 'existing effect of equal or greater'
                    say 'Bah! You are already buffed. Stop wasting my time!'
                    wait 2250
                    say 'Off with you!'
                    wait 1500
                    emote 'slams spellbook closed'
                else             
                    cast 'Arch Protection'
                    wft 6000
                    target player
                    wait 2000
                    
                    say 'You are ready for an adventure.'
                    wait 1500
                    say 'Now shoo!'
                    wait 1500
                    emote 'closes spellbook'
                    
                    createtimer playerID
                    settimer playerID 0
                endif

                break
                
            elseif insysmsg 'too far away.' or insysmsg 'target is out of range'
                overhead 'not in range'
                hotkey "Cancel Current Target"
                break
            else
                wait 100
            endif
        endwhile
    else
        overhead 'Scissors are required for a range check' 38
    endif
else
    overhead 'Target: None' 908
    wait 250
endif

loop