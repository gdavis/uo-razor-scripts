# set this to 0 if you do not want greetings
@setvar! useGreetings 1
@setvar! greetingTimeout 30000


clearsysmsg 

# Ban Bot ----
# ban murderers
hotkey 'Next Murderer Player Target'
if insysmsg 'New target set'
    say 'I ban thee'
    wft 2000
    target lasttarget
    wait 600
    loop
endif

# ban criminals
hotkey 'Next Criminal Player Target'
if insysmsg 'New target set'
    say 'I ban thee'
    wft 2000
    target lasttarget
    wait 600
    loop
endif

# ban greys (blues grey from entering)
hotkey 'Next Grey Player Target'
if insysmsg 'New target set'
    say 'I ban thee'
    wft 2000
    target lasttarget
    wait 600
    loop
endif


# Timers -----

if not timerexists doorMan
    createtimer doorMan
    settimer doorMan 999999
endif

if timer doorMan > 10000
    overhead 'Doorman running...' 54
    settimer doorMan 0
endif

if not timerexists greetingTimer
    createtimer greetingTimer
    settimer greetingTimer greetingTimeout
endif

# Doorman script for NEW! GH
# check for the 3 tiles behind the door
@setvar! doorOpen 1766
@setvar! doorClosed 1765
if position 4313 808 or position 4314 808 
    # check for the opened door
    if findtype doorOpen ground -1 -1 2
        
        if useGreetings = 1 and timer greetingTimer >= greetingTimeout
            clearsysmsg
            settimer greetingTimer 0
        
            @setvar! aphraID 0x1A2B97
            @setvar! charID self
            if charID == aphraID
                random 2
                if insysmsg 'Random: 1'
                    say 'Ugh, let me know when the bot wakes up.'
                elseif insysmsg 'Random: 2'
                    say 'Hello, yeah, I got the doors today...'
                    wait 500
                    emote 'sighs'
                endif
            else
                random 8
                if insysmsg 'Random: 1'
                    say 'Welcome to NEW!'
                elseif insysmsg 'Random: 2'
                    say 'Well met, guildmate!'
                elseif insysmsg 'Random: 3'
                    say 'Welcome back!'
                elseif insysmsg 'Random: 4'
                    say 'Hullo there, friend.'
                elseif insysmsg 'Random: 5'
                    say 'Let me get that door for you.'
                elseif insysmsg 'Random: 6'
                    say 'Greetings!'
                elseif insysmsg 'Random: 7'
                    say 'Please wipe your shoes.'
                elseif insysmsg 'Random: 8'
                    say 'Remember to repair your gear!'
                endif
            endif
        endif
    
        overhead 'Shutting door...' 44
        wait 500
        if findtype doorOpen ground -1 -1 2 as door
            dclick door
        endif
        wait 69
        loop
    elseif findtype doorClosed ground -1 -1 2 
        #overhead 'Door closed.'
        wait 100
        loop
    else
        overhead 'Door not found!' 38
        sysmsg 'You do not appear to be in the NEW! GH.'
        stop
    endif
else 
    overhead 'Waiting to get into position...' 908
    wait 250
    loop
endif