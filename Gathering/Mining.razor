# Move stuff to donkey

### Configuration -------------

## Character Preferences
# Change the variables below to
# the character doing the mining.

# change this if you want to 
# use a pack llama or horse
@setvar! usePackie 0

# enter a weight that is 
# 2 less than max to not go overweight.
@setvar! maxWeight 488



################################
## !! DEVELOPER AREA BELOW !! ##
## !! PROCEED WITH CAUTION !! ##

## Script Init ------------------

# isActive is set to true for the duration of the script,
# and set to false whenever we reach and endpoint
# and need to loop back to the start. this
# gives us a mechanism to know if the script
# was stopped during execution and needs 
# the state variables reset to start a new run.
if not varexist isActive
    @setvar! isActive 0
endif

if isActive = 1
    removetimer 'digTimeout'
    removetimer 'digTime'
    @setvar! isDigging 0
    @setvar! hasCaptcha 0
    @setvar! isActive 0
    overhead 'Starting Miner' 24
endif


@setvar! actionDelay 250
@setvar! digDuration 667
@setvar! digWait 5000
@setvar! isActive 1


if usePackie == 1 and not varexist packie
    overhead 'Select or pack horse or llama:'
    setvar packie
endif

if not timerexists 'digTimeout'
    createtimer 'digTimeout'
endif

if not timerexists 'digTime'
    createtimer  'digTime'
endif

if not varexist isDigging
    @setvar! isDigging 0
endif

if not varexist hasCaptcha
    @setvar! hasCaptcha 0
endif

## Start ----------------------    

#sysmsg "Im mining"

# TODO: Refactor this into another script.
# Its too slow to loop every time and try to smelt,
# and I haven't figured out how to detect
# a nearby forge yet.

if findtype 'forge' ground -1 -1 2 as forge
endif

# try to smelt ore if we are near a forge
#while findtype 'iron ore' as ore
#    getlabel ore oreName
#    overhead '*smelts {{oreName}}*' 44
#    dclick ore
#    wait actionDelay
#    
#    if insysmsg 'you do not see'
#        break
#    endif
#endwhile

# Ask user to provide the recall rune
#if skill 'magery' >= 60 and not varexist 'recallTarget'
#    overhead 'Target the recall runebook or rune to use:'
#    setvar 'recallTarget'
#endif
#
## Check if the stored rune 
## is actually reachable
#if not find 'recallTarget' backpack 
#    overhead 'Stored recall target not found!' 38
#    overhead 'Target a new recall runebook or rune to use:'
#    
#    unsetvar 'recallTarget'
#    setvar 'recallTarget'
#endif

# PK check
if insysmsg 'Now tracking:' or insysmsg 'Distance to destination'
    clearsysmsg 
    if skill 'magery' >= 60
        overhead 'Get out of here!' 34
        hotkey 'Recall'
        waitfortarget         
        target 'recallTarget'
    elseif skill 'Hiding' >= 50
        overhead 'Hiding from PK!' 34
        sysmsg 'Hiding from nearby PK!' 34
        script 'Vanish'
    else
        overhead 'PK Detected!' 38
    endif
endif

# wait for world save
if not varexist isWorldSaving
    @setvar! isWorldSaving 0
endif

if insysmsg 'The world will save'

    if insysmsg 'World save complete'
        overhead 'Save complete, continuing...' 53
        clearsysmsg 
        @setvar! isWorldSaving 0
    else
        @setvar! isWorldSaving 1    
    
        if not hidden and skill 'hiding' > 50
            skill 'Hiding'
        endif
        
        @setvar! isActive 0
        replay
    endif
    
    @setvar! isActive 0
    replay
endif

# check if bags are full
if weight >= maxWeight
    overhead 'Carrying too much!' 38
    sysmsg 'Carrying too much!' 38
    
    if not hidden and skill 'hiding' > 50
        skill 'Hiding'
    endif
    
    # move ore to the packie
    if usePackie == 1 and findtype 'iron ore' backpack as ore
        if find packie ground -1 -1 2
            lift ore 60000
            drop packie -1 -1 -1
            wait actionDelay
        else 
            overhead 'Packie is out of range!' 38
        endif
    else
        wait actionDelay
        @setvar! isActive 0
        replay    
    endif
endif

# equip a pickaxe
if rhandempty
    if not findtype 'pickaxe' backpack
        overhead 'Out of Pickaxes' 38
        wait 5000
        @setvar! isActive 0
        replay
    else
        overhead 'Equipping Pickaxe' 53
        dclicktype 'pickaxe' backpack
        wait 1000    
    endif
endif


# check if we need to stop for captcha
if hasCaptcha = 1
    if insysmsg 'Captcha successful'
        overhead 'CAPTCHA PASSED'
        @setvar! hasCaptcha 0
        settimer 'digTimeout' 0
        settimer 'digTime' 0
    else
        overhead 'Waiting for captcha...' 44
        wait actionDelay
    endif
    
    @setvar! isActive 0
    replay
endif


# use the pickaxe
if isDigging = 0 and hasCaptcha = 0
    #overhead 'new dig'

    if not hidden
        if findtype 'pickaxe' self as pick
            clearsysmsg 
            overhead ' '
            overhead '*swings pickaxe*' 920
            hotkey 'Use item in hand'
            
            settimer 'digTimeout' 0
            settimer 'digTime' 0
            @setvar! isDigging 1
            wait actionDelay
        else
            overhead 'No pickaxes!' 38
        endif
    else
        overhead 'Waiting...' 902
        wait actionDelay
        @setvar! isActive 0
        replay
    endif
endif


if isDigging = 1 and timer 'digTime' > digDuration
    
    if timer 'digTimeout' > digWait
        overhead 'Dig timed out!' 38
        overhead 'Captcha?' 38
        @setvar! hasCaptcha 1
        
        if not hidden and skill 'hiding' > 50
            skill 'Hiding'
        endif
        
        @setvar! isActive 0
        replay
    endif

    if insysmsg 'You do not see any harvestable'
        overhead 'EMPTY' 923
        sysmsg 'No ore here to mine.' 138
        
        removetimer 'digTimeout'
        clearsysmsg
        @setvar! isDigging 0
        
        if not hidden and skill 'hiding' > 50
            skill 'Hiding'
        endif
    
        @setvar! isActive 0
        replay
    endif
    
    # check if we were successful
    if insysmsg 'You dig some'
        overhead 'SUCCESS' 68
        removetimer 'digTimeout'
        clearsysmsg 
        @setvar! isDigging 0
        @setvar! isActive 0
        replay
    endif

    # check if we fail
    if insysmsg 'You loosen some rocks'
        overhead 'YOU FAILED' 38
        removetimer 'digTimeout'    
        clearsysmsg 
        @setvar! isDigging 0
        @setvar! isActive 0
        replay
    endif

    if insysmsg 'That is too far away'
        overhead 'Moving...' 24
        removetimer 'digTimeout'   
        clearsysmsg 
        @setvar! isDigging 0 
        @setvar! isActive 0
        replay
    endif

#    if insysmsg 'You must wait'
#        overhead 'Too fast?' 44
#        #removetimer 'digTimeout'    
#        clearsysmsg 
#        replay
#    endif
else
    wait actionDelay
endif

@setvar! isActive 0
replay