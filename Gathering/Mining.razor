# Move stuff to donkey

### Configuration -------------

# change this if you want to 
# use a pack llama or horse
@setvar! usePackie 0
@setvar! maxWeight 475

if usePackie == 1 and not varexist packie
    overhead 'Select or pack horse or llama:'
    setvar packie
endif

## Start ----------------------

# PK check
if insysmsg 'Now tracking:' or insysmsg 'Distance to destination'
    clearsysmsg 
    if skill 'magery' >= 60
        overhead 'Get out of here!' 34
        hotkey 'Recall'
        waitfortarget         
        target 'recall_runebook'
    elseif skill 'Hiding' >= 50
        overhead 'Hiding from PK!' 34
        sysmsg 'Hiding from nearby PK!' 34
        script 'Vanish'
    endif
endif

# wait for world save
if insysmsg 'The world will save'
    if insysmsg 'World save complete'
        overhead 'Save complete, continuing...' 53
        clearsysmsg 
        loop
    endif
    
    # wait 20 seconds before continuing. 15s for warning + 3-5 seconds for actual save
    for 3
        overhead 'Waiting for world save...' 53
        wait 6667
    endfor
    loop
endif

# check if bags are full
if weight >= maxWeight
    overhead 'Carrying too much!' 38
    sysmsg 'Carrying too much!' 38
    
    # move ore to the packie
    if usePackie == 1 and findtype 'iron ore' backpack as ore
        if find packie ground -1 -1 2
            lift ore 60000
            drop packie -1 -1 -1
        else 
            overhead 'Packie is out of range!' 38
        endif
    else
        wait 5000
        replay    
    endif
endif

# equip a pickaxe
if rhandempty
    if not findtype 'pickaxe' backpack
    overhead 'Out of Pickaxes' 38
        wait 5000
        replay
    else
        overhead 'Equipping Pickaxe' 53
        dclicktype 'pickaxe' backpack
        wait 1000    
    endif
endif

# use the pickaxe
if not hidden
    clearsysmsg 
    overhead 'Digging...' 53
    hotkey 'Use item in hand'
    wait 1500
else
    overhead 'Hidden, waiting to move or for PK' 903
    wait 5000
    replay
endif

# check if spot is empty
if insysmsg 'You do not see any harvestable'
    overhead 'Move!' 43
    sysmsg 'Move!' 43
    
    if not hidden
        skill 'Hiding'
    endif
    
    wait 3000
    replay
endif

# check if we were successful
if insysmsg 'You dig some'
    overhead 'Got some!' 68
    replay
endif

# check if we fail
if insysmsg 'You loosen some rocks'
    overhead 'Trying again...' 903
    replay
endif

if insysmsg 'That is too far away'
    overhead 'Moving...'
    wait 2500
    replay
endif

if insysmsg 'You must wait'
    wait 2500
    replay
endif

# if the above system messages are not present, there may be a captcha check
overhead 'Captcha Check!' 38
sysmsg 'Captcha Check!' 38
script 'Vanish'