# Miner by Aphlux
# 
# Setup:
# - Update the maxWeight below to match your character
#
# Features:
# - Non-blocking wait times
# - Enable tracking skill on start
# - PK detection
# - Automatic Recall Esacpe
# - Configurable Weight
# - Packie Support
# - Pause and Hide during World Save
# - Smelt ore when in range of forge
# - Captcha Detection
# - Auto-equip Pickaxe
# - Prints type of ore mined
# - Overhead feedback
# - Hide when the node is empty
# - Move smelted ore to container

### User Settings --------------
# Change the variables below to
# the character doing the mining.

# change this if you want to 
# use a pack llama or horse
@setvar! usePackie 0

# enter a weight that is 
# 2 less than max to not go overweight.
@setvar! maxWeight 645



################################
## !! DEVELOPER AREA BELOW !! ##
## !! PROCEED WITH CAUTION !! ##

## Script Init ------------------

# isActive is set to true for the duration of the script,
# and set to false whenever we reach and endpoint
# and need to loop back to the start. this
# gives us a mechanism to know if the script
# was stopped during execution and needs 
# the state variables reset to start a new run.
if not varexist isActive
    @setvar! isActive 0
    clearsysmsg 
endif

# check if the previous script was interrupted
if isActive = 1
    clearsysmsg 

    removetimer 'digTime'
    removetimer 'waitAlert'
    removetimer equipTimer
    removetimer hideTimer
    
    @setvar! isDigging 0
    @setvar! isActive 0
    @setvar! isWorldSaving 0
    @setvar! isEquipping 0
    @setvar! shouldHide 0
    
    overhead 'Starting Miner' 24
endif

# constants
@setvar! actionDelay 250
@setvar! digDuration 500
@setvar! digWait 6500
@setvar! messageThrottle 3000
@setvar! pickaxeID 3718
@setvar! hideDelay 3000

# mark script as running
@setvar! isActive 1

if usePackie == 1 and not varexist packie
    overhead 'Select or pack horse or llama:'
    setvar packie
endif

if not timerexists 'digTime'
    createtimer  'digTime'
endif

if not timerexists hideTimer
    createtimer hideTimer
endif

if not varexist isDigging
    @setvar! isDigging 0
endif

if not varexist ingotBag
    overhead 'Select a container to place smelted ingots:'
    setvar ingotBag
endif

if not timerexists 'waitAlert'
    createtimer 'waitAlert'
endif


## Start ----------------------    
#sysmsg "Im mining"

if dead
    overhead 'YOU DIED' 38
    sysmsg 'YOU DIED' 38
    sysmsg 'Stopping Miner.' 24
    @setvar! isActive 0
    stop
endif


## Enable Tracking Buff
@setvar! tracking_gump_id 4267467659

if skill 'Tracking' >= 50 and not findbuff 'Tracking'
    overhead 'Setup tracking...' 48    

    while not gumpexists tracking_gump_id
        sysmsg 'Opening tracking gump...' 53
        skill 'tracking'
        waitforgump tracking_gump_id 5000
    endwhile

    if gumpexists tracking_gump_id
        if not ingump 'Stop Hunting' 4267467659 # NOTE: using tracking_gump_id here causes a script error
            overhead 'Started tracking' 65
            sysmsg 'Tracking players enabled' 65
            gumpresponse 6
            wait actionDelay
        else
            overhead 'Already Hunting' 55
            sysmsg 'Tracking already enabled' 55
        endif
        
        gumpclose tracking_gump_id
    endif
endif


## World Save -----------------

if not varexist isWorldSaving
    @setvar! isWorldSaving 0
endif

if insysmsg 'The world is saving' or isWorldSaving = 1

    if insysmsg 'World save complete'
        overhead 'Save complete, continuing...' 53
        clearsysmsg 
        @setvar! isWorldSaving 0
    elseif isWorldSaving = 0
        @setvar! isWorldSaving 1    
        @setvar! shouldHide 1
    endif
    
    @setvar! isActive 0
    replay
endif


## Recall Escape --------------

# Ask user to provide the recall rune
if not varexist recallTarget or recallTarget = 0
    overhead 'Target the recall runebook or rune to use:'
    @setvar recallTarget 0
    setvar recallTarget
endif

# Check if the stored rune 
# is actually reachable
if recallTarget = 0
    overhead 'Failed to set a recall target!' 38
    loop
elseif find recallTarget backpack
    getlabel recallTarget targetName
    @setvar! foundRecallable 0
    
    @clearignore 
    while findtype 7956|29103|8901 backpack as recallable
        if recallable == recallTarget
            @setvar! foundRecallable 1
            break
        endif
        @ignore recallable
    endwhile
    @clearignore
    
    if foundRecallable = 0
        sysmsg 'WARNING: Recall target is not set to a rune, runebook, or rune tome!' 54
    endif
else
    # rune targets are not found in the backpack
    # for whatever reason, so we need do 
    # search for it by type and see if it exists
    @setvar! foundRune 0
    @clearignore
    while foundRune = 0 and findtype 'recall rune' backpack as rune
        @ignore rune
        if rune = recallTarget
            # overhead 'Found valid recall target' 69
            @setvar! foundRune 1
            break
        endif
    endwhile
    
    if foundRune = 0 
        overhead 'Failed to find a valid recall target!' 38
        @setvar recallTarget 0
        loop
    endif    
endif


# PK check
if insysmsg 'Now tracking:' or insysmsg 'Distance to destination'
    overhead 'PK WARNING - ESCAPE'

    # if you have a custom recall script, 
    # replace this value with yours. otherwise,
    # we will try to use the recall target and recall.
    script 'RecallInn'

    if skill 'magery' >= 60
        overhead 'Time to leave!' 34
        cast 'Recall'
        waitfortarget 6000
        target 'recallTarget'
    else
        overhead 'PK Detected!' 38
    endif
    
    if skill 'Hiding' >= 50 
        while not hidden and not dead
            skill 'hiding'
            wait actionDelay
        endwhile
    endif
    
    sysmsg 'Miner stopped after Escaping PK!' 44
    @setvar! isActive 0
    stop
endif


## Hiding
if shouldHide = 1 and timer hideTimer > hideDelay
    if not hidden and skill 'hiding' > 50
        skill 'Hiding'
        wait actionDelay
    endif
    
    if hidden 
        @setvar! shouldHide 0
    endif
endif


## Smelt at Forge ---------------
# Look for a forge within range.
# If found, smelt all the ore
# that we have in the backpack
if findtype 'forge' ground -1 -1 2
    @setvar! didSmelt 0
    @setvar! isDigging 0

    while findtype 'iron ore' backpack as ore
        getlabel ore oreName
        overhead '*smelts {{oreName}}*' 44
        dclick ore
        @setvar! didSmelt 1
        wait 1250
        
        if insysmsg 'you do not see'
            # moved out of range
            break
        endif
    endwhile
    
    # place ingots into bag
    if didSmelt = 1 and varexist ingotBag
        @clearignore 
        while findtype 'iron ingot%s' backpack as ingots    
            if find ingots ingotBag
                @ignore ingots
                continue
            endif
        
            getlabel ingots ingotName
            overhead 'Stashing: {{ingotName}}'
            sysmsg '{{ingotName}}'
            
            lift ingots 60000
            drop ingotBag -1 -1 -1
            wait actionDelay
            @ignore ingots
        endwhile
        @clearignore 
    endif
endif


## Check Weight ----------------
# check if bags are full
if weight >= maxWeight
    
    if timer 'waitAlert' > messageThrottle
        settimer 'waitAlert' 0
        overhead 'Carrying too much!' 38
        sysmsg 'Carrying too much!' 38
    endif
    
    if not hidden and skill 'hiding' > 50
        skill 'Hiding'
    endif
    
    # TODO: need to check for packie being full
    # move ore to the packie
    if usePackie == 1 and findtype 'iron ore' backpack as ore
        if find packie ground -1 -1 2
            lift ore 60000
            drop packie -1 -1 -1
            wait actionDelay
        else 
            overhead 'Packie is out of range!' 38
        endif
    else
        wait actionDelay

        @setvar! isActive 0
        replay    
    endif
endif


## Pickaxe --------------------
getlabel righthand rhandItem
@setvar! pickaxeEquipped 0
if 'pickaxe' in rhandItem
    @setvar! pickaxeEquipped 1
endif

# timer started after equipping a new pickaxe.
# used to fix an issue where immediately
# trying to use the axe would
# not work if it was just equipped.
@setvar! equipDelay 3000
if not timerexists equipTimer
    createtimer equipTimer
endif

if isEquipping = 1 
    if timer equipTimer < equipDelay
        overhead 'waiting to equip' 44
        wait actionDelay
        replay
    else 
        @setvar! isEquipping 0
    endif
endif

if rhandempty or pickaxeEquipped = 0
    if not findtype pickaxeID backpack
        overhead 'Out of Pickaxes' 38
        
        if not hidden and skill 'hiding' > 50
            skill 'Hiding'
            wait actionDelay
        endif
        
        @setvar! isActive 0
        replay
    else 
        overhead 'Equipping Pickaxe' 53
        dclicktype pickaxeID backpack
        settimer equipTimer 0
        @setvar! isEquipping 1
        @setvar! isActive 0
        replay
    endif
endif

## Do the dig ----------------
# use the pickaxe
if isDigging = 0 and isEquipping = 0
    #overhead 'new dig'

    if not hidden
        if findtype pickaxeID righthand as pick
            overhead ' '
            overhead '*swings pickaxe*' 645
            dclick pick
            
            settimer 'digTime' 0
            @setvar! isDigging 1
            wait actionDelay
        else
            overhead 'No pickaxes!' 38
        endif
    else
        # only show an overhead every few seconds
        if timer 'waitAlert' > messageThrottle
            overhead 'Waiting...' 902
            settimer 'waitAlert' 0
        endif
        
        wait actionDelay
        @setvar! isActive 0
        replay
    endif
endif


## Dig Result -----------------
# check results after digging
if isDigging = 1 and timer 'digTime' > digDuration

    if insysmsg 'Harvesting is not allowed'
        overhead 'Cannot mine here!' 38  
        
        @setvar! isDigging 0
        @setvar! isActive 0
        @setvar! shouldHide 1
        replay
    endif
    
    if insysmsg 'You do not see any harvestable'
        overhead 'EMPTY' 923
        sysmsg 'No ore here to mine.' 138
        
        @setvar! shouldHide 1
        @setvar! isDigging 0
        @setvar! isActive 0
        replay
    endif
    
    # check if we fail
    if insysmsg 'You loosen some rocks'
        overhead 'YOU FAILED' 38

        @setvar! isDigging 0
        @setvar! isActive 0
        replay
    endif
    
    # check if we were successful
    @setvar! digSuccess 0
    
    if insysmsg 'some iron ore'
        overhead 'IRON' 924
        @setvar! digSuccess 1
    elseif insysmsg 'some dull'
        overhead 'DULL COPPER' 442
        @setvar! digSuccess 1
    elseif insysmsg 'some shadow'
        overhead 'SHADOW IRON' 902
        @setvar! digSuccess 1
    elseif insysmsg 'some copper'
        overhead 'COPPER' 444
        @setvar! digSuccess 1
    elseif insysmsg 'some bronze'
        overhead 'BRONZE' 44
        @setvar! digSuccess 1
    elseif insysmsg 'some gold'
        overhead 'GOLD' 53
        @setvar! digSuccess 1
    elseif insysmsg 'some agapite'
        overhead 'AGAPITE' 36
        @setvar! digSuccess 1
    elseif insysmsg 'some verite'
        overhead 'VERITE' 68
        @setvar! digSuccess 1
    elseif insysmsg 'some valorite'
        overhead 'VALORITE' 8
        @setvar! digSuccess 1
    elseif insysmsg 'some avarite'
        overhead 'AVARITE' 14
        @setvar! digSuccess 1
    endif
    
    if digSuccess = 1
        @setvar! shouldHide 0
        @setvar! isDigging 0
        @setvar! isActive 0
        replay
    endif
    
    # recent travel
    if insysmsg 'You have recently traveled' or insysmsg 'You have recently teleported'
        overhead 'Travel Cooldown...' 24

        @setvar! shouldHide 1
        @setvar! isDigging 0
        @setvar! isActive 0
        replay
    endif

    if insysmsg 'That is too far away'
        overhead 'Moving...' 24

        @setvar! shouldHide 0
        @setvar! isDigging 0 
        @setvar! isActive 0
        replay
    endif

    if insysmsg 'You must wait'
        overhead 'Too fast?' 44

        @setvar! isActive 0
        replay
    endif
else
    wait actionDelay
endif

if insysmsg 'world will save'
endif

@setvar! isActive 0
replay