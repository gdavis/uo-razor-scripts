# Dumps loot bag into resource pile
# Dumps magic items into bag
# - use "unidentified" in the label
# Resupplies from the shelf
# Creates shrooms
# Eat food if hungry
@clearignore 
@setvar! shroomCount 24
@setvar! delay 666

# be at home
if not position 247 455
    overhead 'Not home!' 38
    stop
endif

# setup targets
if not varexist lootBag or not find lootBag self
    overhead 'Select your loot container:'
    setvar lootBag
endif

if not varexist magicItemBag 
    overhead 'Select the bag for unidentified magic items:'
    setvar magicItemBag
endif

# add loot to the resource stockpile
if findtype 'resource container' ground -1 -1 2 as pile
    # add loot to the pile    
    menu pile 0
    wft 6000
    target lootBag
    wait delay
    overhead 'Resources stored.' 53
else
    overhead 'missing stockpile or shelf!' 38
endif

# restock from the shelf
if findtype 'storage shelf' ground -1 -1 2 as shelf
    # add reagents from the satchel.
    # we do this becaues we are poor and still pickup regs.
    # to save space the regs go in the satchel, so we
    # are likely carrying more than we want to for 
    # the next dungeon run.
    if findtype 'reagent satchel' self as satchel
        menu satchel 0
        wait 250
        menu shelf 0
        wft 6000
        target satchel
        wait delay
        overhead 'Satchel emptied.' 53
    endif    

    menu shelf 1
    wait delay
    overhead 'Resupplied.' 53
    gumpclose 3232825965
else 
    overhead 'missing storage shelf!' 38
endif

# move loot to magic item bag
# list of loot items to unload
# TODO: need to add all the things
removelist basicItems
createlist basicItems
# magery
pushlist basicItems 'spellbook'
pushlist basicItems 'wand'
# archery
pushlist basicItems 'bow'
pushlist basicItems 'crossbow'
pushlist basicItems 'heavy crossbow'
# martial
pushlist basicItems 'martial arts scroll'
# barding
pushlist basicItems 'lute'
pushlist basicItems 'tambourine'
pushlist basicItems 'drum'
pushlist basicItems 'lap harp'
pushlist basicItems 'bamboo flute'
# fencing
pushlist basicItems 'Long Spear'
pushlist basicItems 'war fork'
pushlist basicItems 'short spear'
pushlist basicItems 'kryss'
# leather
pushlist basicItems 'leather gorget'
pushlist basicItems 'leather shorts'
pushlist basicItems 'Leather Gloves'
pushlist basicItems 'leather sleeves'
pushlist basicItems 'leather skirt'
pushlist basicItems 'leather leggings'
pushlist basicItems 'leather armor'
pushlist basicItems 'leather bustier'
pushlist basicItems 'leather tunic'
# shields
pushlist basicItems 'kite shield'
pushlist basicItems 'wooden shield'
pushlist basicItems 'bronze shield'
pushlist basicItems 'heater shield'
pushlist basicItems 'buckler '
pushlist basicItems 'metal shield'
pushlist basicItems 'heater shield'
# platemail
pushlist basicItems 'norse helm'
pushlist basicItems 'close helm'
pushlist basicItems 'bascinet'
pushlist basicItems 'helmet'
pushlist basicItems 'plate helm'
pushlist basicItems 'platemail gorget'
pushlist basicItems 'plate armor'
pushlist basicItems 'platemail gloves'
pushlist basicItems 'platemail arms'
pushlist basicItems 'platemail'
pushlist basicItems 'platemail legs'
# chainmail
pushlist basicItems 'chainmail coif'
pushlist basicItems 'chainmail leggings'
pushlist basicItems 'chainmail tunic'
# bone
pushlist basicItems 'bone helmet'
pushlist basicItems 'bone arms'
pushlist basicItems 'bone gloves'
pushlist basicItems 'bone legs'
pushlist basicItems 'bone armor'
# ringmail
pushlist basicItems 'ringmail tunic'
pushlist basicItems 'ringmail sleeves'
pushlist basicItems 'ringmail gloves'
pushlist basicItems 'ringmail leggings'
pushlist basicItems 'ringmail gloves'
# studded leather
pushlist basicItems 'leather cap'
pushlist basicItems 'studded armor'
pushlist basicItems 'studded leggings'
pushlist basicItems 'studded tunic'
pushlist basicItems 'studded gorget'
pushlist basicItems 'studded sleeves'
pushlist basicItems 'studded bustier'
pushlist basicItems 'studded gloves'
# swords
pushlist basicItems 'bardiche'
pushlist basicItems 'halberd'
pushlist basicItems 'bardiche'
pushlist basicItems 'longsword'
pushlist basicItems 'katana'
pushlist basicItems 'pitchfork'
pushlist basicItems 'large battle axe'
pushlist basicItems 'two handed axe'
pushlist basicItems 'battle axe'
pushlist basicItems 'viking sword'
pushlist basicItems 'broadsword'
pushlist basicItems 'double axe'
pushlist basicItems 'war axe'
pushlist basicItems "executioner's axe"
pushlist basicItems 'axe'
pushlist basicItems 'cutlass'
pushlist basicItems 'scimitar'
# maces
pushlist basicItems "shepherd's crook"
pushlist basicItems 'war mace'
pushlist basicItems 'mace'
pushlist basicItems 'maul'
pushlist basicItems 'hammer pick'
pushlist basicItems 'quarter staff'
pushlist basicItems 'gnarled staff'
pushlist basicItems 'black staff'
pushlist basicItems 'war hammer'
pushlist basicItems 'bladed staff'
pushlist basicItems 'club'
pushlist basicItems 'quarter staff'
# pushlist basicItems ''

# open rares lockbox
clearsysmsg 
if findtype 'wooden box' backpack as box and findtype 'copper key' backpack as key
    dclick key
    wft 6000
    target box
    wait delay
    
    dclick box
    wait delay
    
    if insysmsg 'It appears to be locked'
        overhead 'Locked.' 87
        dclick key
        wft 6000
        target box
        wait delay 
    else
        overhead 'Unlocked.' 44
    endif
    
    if gumpexists 736038070
        gumpclose 736038070
    endif
endif

# dump magic items into recycler
if findtype 'ornate elven chest' ground any any 2 as itemBox
    overhead 'Unloading unidentified items...' 53
    menu itemBox 1
else
    # unload listed items not dumped into recylcer
    clearsysmsg 
    @clearignore 
    @setvar! foundItems 0
    foreach itemID in basicItems
        while findtype itemID lootBag as item
            if foundItems = 0
                overhead 'Unloading items...' 53
                @setvar! foundItems 1
            endif
            
            getlabel item itemName
            if 'unidentified' in itemName
                overhead "Moving: '{{itemName}}'" 444
                lift item
                drop magicItemBag -1 -1 -1
                wait 250
                
                if insysmsg 'That container cannot hold more'
                    overhead 'Loot bag is full!' 38
                    stop
                endif
            endif
            
            @ignore item
        endwhile
    endfor
    @clearignore
endif

# add treasure maps to tome
if findtype 'book' ground 2990 any any 2 as tome and findtype 'map' backpack 0
    overhead 'Unloading treasure maps...' 53
    menu tome 0
endif

# add lumber maps to tome
if findtype 'book' ground 2799 any any 2 as tome and findtype 'map' backpack 2799
    overhead 'Unloading lumber maps...' 53
    menu tome 0
endif

# add skinning maps to tome
if findtype 'book' ground 2651 any any 2 as tome and findtype 'map' backpack 2651
    overhead 'Unloading skinning maps...' 53
    menu tome 0
endif

# add ore maps to tome
if findtype 'book' ground 2796 any any 2 as tome and findtype 'map' backpack 2796
    overhead 'Unloading ore maps...' 53
    menu tome 0
endif

# add fishing maps to tome
if findtype 'book' ground 2722 any any 2 as tome and findtype 'map' backpack 2904
    overhead 'Unloading fishing maps...' 53
    menu tome 0
endif

# add rare cloth to tome
if findtype 'book' ground 1495 any any 2 as tome and findtype 'folded cloth' backpack
    overhead 'Unloading rare cloth...' 53
    menu tome 0
endif

# add skill scrolls to tome
if findtype 'book' ground 2963 any any 2 as tome and findtype 'scroll of calling' backpack
    overhead 'Unloading skill scrolls...' 53
    menu tome 0
endif

# arcane runes to tome
if findtype 'book' ground 2085 any any 2 as tome and findtype 'rune' backpack
    overhead 'Unloading arcane runes...' 53
    menu tome 0
endif

# add plant stuff to shelf
if findtype 'garden shelf' ground any any any 2 as shelf or findtype 'golden garden shelf' ground any any any 2 as shelf
    menu shelf 0
    wft 3000
    target self
endif

# eat foot if hungry
if not findbuff 'Food Satisfaction'
    if findtype 'tray' backpack as food 
        dclick food
        overhead 'NOM NOM NOM' 44
    else
        overhead 'HUNGRY! Need Food!' 38
    endif
else 
    overhead 'Not hungry.' 902
endif

# add gold to deposit box
if findtype 'bank deposit safe' ground any any 2 as bank
    while findtype 'gold coin' backpack as cash
        getlabel cash cashName
        overhead 'Depositing: {{cashName}}' 54
        lift cash 60000
        drop bank
        wait delay
    endwhile
    
    while findtype 'muffins' backpack as dubs
        getlabel dubs cashName
        overhead 'Depositing: {{cashName}}' 54
        lift dubs 60000
        drop bank
        wait delay
    endwhile
endif

# DONE
overhead 'Locked & Stocked!' 68