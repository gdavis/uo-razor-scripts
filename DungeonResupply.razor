# Dumps loot bag into resource pile
# Dumps magic items into bag
# - use "unidentified" in the label
# Resupplies from the shelf
# Creates shrooms
# Eat food if hungry

@setvar! shroomCount 24
@setvar! delay 666

# be at home
if not position 247 455
    overhead 'Not home!' 38
    stop
endif

# setup targets
if not varexist lootBag
    overhead 'Select your loot container:'
    setvar lootBag
endif

if not find lootBag self
    overhead 'Could not find loot bag. Retarget:'
    setvar lootBag
endif

if not varexist magicItemBag 
    overhead 'Select the bag for unidentified magic items:'
    setvar magicItemBag
endif

# add loot to the resource stockpile
if findtype 'resource container' ground -1 -1 2 as pile
    # add loot to the pile    
    menu pile 0
    wft 6000
    target lootBag
    wait delay
    overhead 'Resources stored.' 53
else
    overhead 'missing stockpile or shelf!' 38
endif

# restock from the shelf
if findtype 'storage shelf' ground -1 -1 2 as shelf
    # add reagents from the satchel.
    # we do this becaues we are poor and still pickup regs.
    # to save space the regs go in the satchel, so we
    # are likely carrying more than we want to for 
    # the next dungeon run.
    if findtype 'reagent satchel' self as satchel
        menu satchel 0
        wait 250
        menu shelf 0
        wft 6000
        target satchel
        wait delay
        overhead 'Satchel emptied.' 53
    endif    

    menu shelf 1
    wait delay
    overhead 'Resupplied.' 53
    gumpclose
else 
    overhead 'missing storage shelf!' 38
endif

# move loot to magic item bag
# list of loot items to unload
# TODO: need to add all the things
removelist basicItems
createlist basicItems
# magery
pushlist basicItems 'spellbook'
pushlist basicItems 'wand'
# archery
pushlist basicItems 'bow'
pushlist basicItems 'crossbow'
pushlist basicItems 'heavy crossbow'
# martial
pushlist basicItems 'martial arts scroll'
# barding
pushlist basicItems 'lute'
pushlist basicItems 'tambourine'
pushlist basicItems 'drum'
pushlist basicItems 'lap harp'
pushlist basicItems 'bamboo flute'
# fencing
pushlist basicItems 'Long Spear'
pushlist basicItems 'war fork'
pushlist basicItems 'short spear'
pushlist basicItems 'kryss'
# leather
pushlist basicItems 'leather gorget'
pushlist basicItems 'leather shorts'
pushlist basicItems 'Leather Gloves'
pushlist basicItems 'leather sleeves'
pushlist basicItems 'leather skirt'
pushlist basicItems 'leather leggings'
pushlist basicItems 'leather armor'
pushlist basicItems 'leather bustier'
pushlist basicItems 'leather tunic'
# shields
pushlist basicItems 'kite shield'
pushlist basicItems 'wooden shield'
pushlist basicItems 'bronze shield'
pushlist basicItems 'heater shield'
pushlist basicItems 'buckler '
pushlist basicItems 'metal shield'
pushlist basicItems 'heater shield'
# platemail
pushlist basicItems 'norse helm'
pushlist basicItems 'close helm'
pushlist basicItems 'bascinet'
pushlist basicItems 'helmet'
pushlist basicItems 'plate helm'
pushlist basicItems 'platemail gorget'
pushlist basicItems 'plate armor'
pushlist basicItems 'platemail gloves'
pushlist basicItems 'platemail arms'
pushlist basicItems 'platemail'
pushlist basicItems 'platemail legs'
# chainmail
pushlist basicItems 'chainmail coif'
pushlist basicItems 'chainmail leggings'
pushlist basicItems 'chainmail tunic'
# bone
pushlist basicItems 'bone helmet'
pushlist basicItems 'bone arms'
pushlist basicItems 'bone gloves'
pushlist basicItems 'bone legs'
pushlist basicItems 'bone armor'
# ringmail
pushlist basicItems 'ringmail tunic'
pushlist basicItems 'ringmail sleeves'
pushlist basicItems 'ringmail gloves'
pushlist basicItems 'ringmail leggings'
pushlist basicItems 'ringmail gloves'
# studded leather
pushlist basicItems 'leather cap'
pushlist basicItems 'studded armor'
pushlist basicItems 'studded leggings'
pushlist basicItems 'studded tunic'
pushlist basicItems 'studded gorget'
pushlist basicItems 'studded sleeves'
pushlist basicItems 'studded bustier'
pushlist basicItems 'studded gloves'
# swords
pushlist basicItems 'bardiche'
pushlist basicItems 'halberd'
pushlist basicItems 'bardiche'
pushlist basicItems 'longsword'
pushlist basicItems 'katana'
pushlist basicItems 'pitchfork'
pushlist basicItems 'large battle axe'
pushlist basicItems 'two handed axe'
pushlist basicItems 'battle axe'
pushlist basicItems 'viking sword'
pushlist basicItems 'broadsword'
pushlist basicItems 'double axe'
pushlist basicItems 'war axe'
pushlist basicItems "executioner's axe"
pushlist basicItems 'axe'
pushlist basicItems 'cutlass'
pushlist basicItems "shepherd's crook"
# maces
pushlist basicItems 'war mace'
pushlist basicItems 'mace'
pushlist basicItems 'maul'
pushlist basicItems 'hammer pick'
pushlist basicItems 'quarter staff'
pushlist basicItems 'gnarled staff'
pushlist basicItems 'black staff'
pushlist basicItems 'war hammer'
pushlist basicItems 'bladed staff'
pushlist basicItems 'club'
pushlist basicItems 'quarter staff'
# pushlist basicItems ''

clearsysmsg 
@clearignore 
overhead 'Unloading items...' 53
foreach itemID in basicItems
    while findtype itemID lootBag as item
        getlabel item itemName
        if 'unidentified' in itemName
            overhead "Moving: '{{itemName}}'" 444
            lift item
            drop magicItemBag -1 -1 -1
            wait 250
            
            if insysmsg 'That container cannot hold more'
                overhead 'Loot bag is full!' 38
                stop
            endif
        endif
        
        @ignore item
    endwhile
endfor
@clearignore 
overhead 'Items unloaded.' 53

# create shrooms
if findtype 'wizards grimoire' backpack
    while counttype 'mushroom' self < shroomCount
        clearsysmsg 
        cast 'Create Food'
        wait 1500
        
        if not insysmsg 'You create'
            overhead 'Restock failed!' 38
            stop
        endif
    endwhile
    overhead 'Shrooms restocked' 53
endif

# eat foot if hungry
if not findbuff 'Food Satisfaction'
    if findtype 'tray' backpack as food 
        dclick food
        overhead 'NOM NOM NOM' 44
    else
        overhead 'HUNGRY! Need Food!' 38
    endif
else 
    overhead 'Not hungry.' 902
endif

# DONE
overhead 'Locked & Stocked!' 68