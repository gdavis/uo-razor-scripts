# Looks for encoded maps,
# and decodes them.

@setvar! oreMapGumpID 3367641484
@setvar! tmapGumpID 1520869851
@setvar! lumberMapGumpID 1332805401
@setvar! skinningMapGumpID 3473029237

if not varexist tmapSpyglass or not find tmapSpyglass backpack
    overhead 'Select your spyglass:'
    setvar tmapSpyglass
endif

@clearignore 
while findtype 5356 backpack as tmap
    gumpclose oreMapGumpID
    gumpclose tmapGumpID
    gumpclose lumberMapGumpID
    gumpclose skinningMapGumpID
    wait 500

    getlabel tmap mapName

    @setvar! decodeGumpID 0
    if 'treasure map' in mapName
        @setvar! decodeGumpID tmapGumpID
    elseif 'ore map' in mapName
        @setvar! decodeGumpID oreMapGumpID
    elseif 'lumber map' in mapName
        @setvar! decodeGumpID lumberMapGumpID
    elseif 'skinning map' in mapName
        @setvar! decodeGumpID skinningMapGumpID
    else 
        overhead 'Unknow map type! Aborting.' 38
        sysmsg 'Unknow map type! Aborting.' 38
        stop
    endif
    
    if decodeGumpID == 0 
        overhead 'Unknown map type!' 38
        sysmsg 'Unknown map type!' 38
    endif

    while gumpexists decodeGumpID
        gumpclose decodeGumpID
        wait 150
    endwhile

    if 'undeciphered' in mapName
        overhead 'Deciphering {{mapName}}...'
        dclick tmapSpyglass
        wft
        target tmap
        waitforgump decodeGumpID
        gumpresponse 4 decodeGumpID
        waitforgump decodeGumpID
        wait 3000
        
    else
        dclick tmapSpyglass
        wft 1000
        target tmap
        waitforgump decodeGumpID
        
        if insysmsg 'You have reached the maximum accuracy possible'
            sysmsg 'Map at maximum accuracy.' 54
            @ignore tmap
        elseif ingump 'Improve Map Accuracy'
            overhead 'Improving accuracy...'    
            gumpresponse 4 decodeGumpID
            waitforgump decodeGumpID
            wait 3000
        else
            sysmsg 'Map at maximum accuracy.' 54
            @ignore tmap
        endif
    endif
endwhile