if dead 
    overhead 'Trap made a whoopsie! Need res.' 38
    wait 5000
    loop
endif

clearsysmsg

if not varexist chest
    overhead 'Unlock what chest?' 44
    @setvar! chest
endif

getlabel chest chestName
overhead 'Unlocking chest:' 44
overhead chestName 1001

if not gumpexists 736038070
    dclick chest
    waitforgump 736038070 1000
    
    if not insysmsg 'It appears to be locked'
        overhead 'That chest is not locked'
    endif
endif

if not ingump 'Cleared of Traps'
    overhead 'Has traps...' 908
    gumpresponse 7
    waitforgump 736038070 1000
    wait 1000

    if insysmsg 'You attempt to remove'
        overhead 'Removing traps...'
        wait 1000
        loop
    elseif insysmsg 'You must wait a few moments before attempting to remove traps'
        wait 1000
        loop
    endif
else
    overhead 'Still locked...' 908
    gumpresponse 4
    waitforgump 736038070
    wait 3500
    
    if insysmsg 'You attempt to pick the lock' 
    
        if insysmsg 'You successfully pick the lock'
            overhead 'Lock Picked!' 8
            sysmsg 'Chest unlocked. All done!'
            stop
        elseif insysmsg 'You make some progress on the lock'
            overhead 'Progress!' 8
        elseif insysmsg 'You fail to make any progress' 
            overhead 'No progress...' 902
        else
            overhead 'Too fast??' 38
            wait 1000
        endif
        
        loop
    else
        overhead 'Too fast?' 
        wait 1000
        loop
    endif
endif