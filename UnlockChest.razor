@setvar! lockedChestGumpID 736038070

if dead 
    overhead 'Trap made a whoopsie! Need res.' 38
    warmode on
    wait 5000
    loop
endif

clearsysmsg

if not varexist lockedChest
    overhead 'Unlock what chest?' 44
    setvar lockedChest
endif

getlabel lockedChest chestName
overhead 'Unlocking chest:' 44
overhead chestName 1001

if '(sealed)' in chestName 
else
    unsetvar lockedChest
    overhead 'That chest is not sealed.'
    stop
endif

if not gumpexists lockedChestGumpID
    dclick lockedChest
    waitforgump lockedChestGumpID 1000
    
    if not insysmsg 'It appears to be locked'
        overhead 'That chest is not locked'
        say 'This chest is not locked:'
        wait 1000
        say chestName
    endif
endif

if not ingump 'Cleared of Traps'
    #say 'This chest is dangerous! Wish me luck while I disarm this.'
    overhead 'Has traps...' 908
    gumpresponse 7
    waitforgump lockedChestGumpID 1000
    wait 1000

    if insysmsg 'You attempt to remove'
        overhead 'Removing traps...'
        wait 1000
        loop
    elseif insysmsg 'You must wait a few moments before attempting to remove traps'
        wait 1000
        loop
    endif
else
    overhead 'Still locked...' 908
    gumpresponse 4
    waitforgump lockedChestGumpID
    wait 3500
    
    if insysmsg 'You attempt to pick the lock' 
    
        if insysmsg 'You successfully pick the lock'
            overhead 'Lock Picked!' 8
            sysmsg 'Chest unlocked. All done!'
            @unsetvar! lockedChest
            stop
        elseif insysmsg 'You make some progress on the lock'
            overhead 'Progress!' 8
        elseif insysmsg 'You fail to make any progress' 
            overhead 'No progress...' 902
        else
            overhead 'Too fast??' 38
            wait 1000
        endif
        
        loop
    else
        overhead 'Too fast?' 
        wait 1000
        loop
    endif
endif