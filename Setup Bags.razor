# Organizes bag structure after death.
# Assumes at least 6 trapped pouches are
# in the backpack.
# 3 pouches are filled with kindling
# and places within another trapped
# pouch. 
# The 3 parent pouches are placed
# so two are in the top left
# and the main content bag
# is covered in the bottom left.
# The loot bag (backpack) and supplies (bag)
# are placed in the top level pouch.
@setvar! actionDelay 1000

removelist parentPouches1
removelist parentPouches2
removelist childPouches

createlist parentPouches1
createlist parentPouches2
createlist childPouches

if counttype 'pouch' backpack 38 < 6
    overhead 'ERROR: Expected at least 6 trapped pouches in backpack!' 38
    stop
endif

overhead 'Setting up bags...' 54

# organize pouches into child/parent lists
clearignore 
for 3
    if findtype 'pouch' backpack 38 as parentPouch
        overhead 'adding parent pouch'
        pushlist parentPouches1 parentPouch
        pushlist parentPouches2 parentPouch
        ignore parentPouch
    endif
endfor

for 3
    if findtype 'pouch' backpack 38 as childPouch
        overhead 'adding child pouch'
        pushlist childPouches childPouch
        ignore childPouch
    endif
endfor

clearignore 

# the findtype
# can also return the top
# level bag, which is no good.
@setvar! backpackID backpack
@ignore backpackID

# place kindling in child bags
# and then into parent bag
for 3
    if poplist childPouches front as child
        if findtype 'kindling' backpack as kindling
            overhead 'moving kindling to bag'
            lift kindling 1
            drop child -1 -1 -1
            wait actionDelay
        endif
        
        if poplist parentPouches1 front as parent
            overhead 'moving child to parent'
            lift child 1
            drop parent -1 -1 -1
            wait actionDelay
        endif
    endif
endfor

# move bags into position
if poplist parentPouches2 front as topLeftPouch
    overhead 'moving top left pouch'
    lift topLeftPouch
    drop backpack 44 64 0
    wait actionDelay
endif

# trapped pouch under real bag
if poplist parentPouches2 front as btmLeftPouch
    overhead 'moving lower bottom left pouch'
    lift btmLeftPouch
    drop backpack 44 133 0
    wait actionDelay
endif

# main bag gets the supplies and loot bags added
# and then placed into bottom left of backpack
if findtype 'pouch' backpack 0 as pouch
    
    # move loot bag (backpack) to pouch
    if findtype 3701 backpack 0 as lootBag
        overhead 'moving loot bag to top pouch'
        lift lootBag
        drop pouch 93 65 0
        wait actionDelay
    endif
    
    # move supplies bag to pouch
    if findtype 'bag' backpack 0 as suppliesBag
        overhead 'moving supplies bag to top pouch'
        lift suppliesBag
        drop pouch 135 65 0
        wait actionDelay
    endif

    lift pouch
    drop backpack 44 133 0
    wait actionDelay
endif

# trapped pouch on top of content bag
if poplist parentPouches2 front as btmLeftPouch
    overhead 'moving lower left top pouch'
    lift btmLeftPouch
    drop backpack 44 133 0
    wait actionDelay
endif

# place atlas over bags
if findtype 'atlas' backpack as atlas
    overhead 'moving atlas'
    lift atlas 1
    drop backpack 44 123 0
    wait actionDelay
endif

overhead 'Bag setup done.' 54