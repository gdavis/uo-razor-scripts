# move list of weapons into safe bag
if listexists stashList
    removelist stashList
endif
createlist stashList
## Add item names using >info 
## to stash other weapon types
pushlist stashList 'kryss'
pushlist stashList 'Long Spear'
pushlist stashList 'war fork'
pushlist stashList 'short spear'
pushlist stashList 'crossbow'
pushlist stashList 'heavy crossbow'
pushlist stashList 'spellbook'
pushlist stashList 'bardiche'
pushlist stashList 'viking sword'
pushlist stashList 'katana'
pushlist stashList 'halberd'

# ----------------------------
@clearignore

# 5042 = bow
@setvar weaponID 5042

# check if equipped
if findtype weaponID lefthand
    overhead '+Bow Equipped' 54
    stop
endif

# clear previous item moves
hotkey 'Clear Drag/Drop Queue'

# Equip the weapon
dress 'Bow'

# 2207 == verite
if findtype weaponID lefthand 2207 as weapon 
    overhead '+Verite bow' 14
# 2897 == poison aspect
elseif findtype weaponID lefthand 2897 as weapon 
    overhead '+Poison bow' 36
# 2599 == void aspect
elseif findtype weaponID lefthand 2599 as weapon 
    overhead '+Void bow' 14
# 2504 == war aspect
elseif findtype weaponID lefthand 2503 as weapon
    overhead '+War bow' 36
# 1790 == shadow aspect
elseif findtype weaponID lefthand 1790 as weapon
    overhead '+Shadow bow' 902
# normal
elseif findtype weaponID lefthand as weapon
    overhead '+Reg bow' 920
else
    overhead 'Bow not found!' 38
    stop
endif

# stash items into bag
if findtype 'bag' backpack 0 as myBag 
    @clearignore 
    foreach itemID in stashList
        while findtype itemID backpack as weapon
            getlabel weapon weaponName
            @setvar! shouldStash 0
            
            if 'unidentified' in weaponName
                sysmsg 'Ignoring magic item'
            elseif 'blessed' in weaponName
                sysmsg 'Ignoring blessed item'
            else
                @setvar! shouldStash 1
            endif
            
            if shouldStash = 1 and not find weapon myBag
                lift weapon
                drop myBag -1 -1 -1
                sysmsg 'Stashing: {{weaponName}}' 920
                wait 250
            endif
            
            @ignore weapon
        endwhile
    endfor
    @clearignore
else
    overhead 'NO BAG FOUND!' 38
    stop
endif