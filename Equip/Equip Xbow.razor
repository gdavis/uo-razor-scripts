@clearignore

if findtype 'crossbow' lefthand
    overhead 'Already Equipped!' 54
    stop
endif

# 2599 == void aspect
if findtype 'crossbow' self 2599 as bow 
    dclick bow
    while rhandempty
    endwhile
    overhead 'Void Xbow Equipped' 14
# 2504 == war aspect
elseif findtype 'crossbow' self 2503 as bow 
    dclick bow
    while rhandempty
    endwhile
    overhead 'War Xbow Equipped' 36
# normal
elseif findtype 'crossbow' self as bow
    dclick bow
    while rhandempty
    endwhile
    overhead 'Xbow Equipped' 920
else
    overhead 'Xbow not found!' 38
endif


# try to move other stuff into bag
if findtype 'bag' backpack 0 as myBag 
    # move aspected bow
    @setvar! foundBow 0
    if findtype 'bow' self 2599 as aspectBow
        @setvar! foundBow aspectBow
    elseif findtype 'bow' self 2503 as aspectBow
        @setvar! foundBow aspectBow
    elseif findtype 'bow' self 1790 as aspectBow
        @setvar! foundBow aspectBow
    else 
        while findtype 'bow' self as regBow
            getlabel regBow bowName
            if 'exceptional' in bowName
                @setvar! foundBow regBow
                break
            else
                @ignore regBow
            endif
        endwhile
    endif
    if foundBow > 0 and not find foundBow myBag
        lift foundBow
        drop myBag -1 -1 -1
        #overhead 'stashing bow'
        wait 250
    endif
    
    # move aspected heavy xbow
    @setvar! foundHeavyXbow 0
    if findtype 'heavy crossbow' self 2599 as aspectBow
        @setvar! foundHeavyXbow aspectBow
    elseif findtype 'heavy crossbow' self 2503 as aspectBow
        @setvar! foundHeavyXbow aspectBow
    elseif findtype 'heavy crossbow' self 1790 as aspectBow
        @setvar! foundHeavyXbow aspectBow
    else 
        while findtype 'heavy crossbow' self as regBow
            getlabel regBow bowName
            if 'exceptional' in bowName
                @setvar! foundHeavyXbow regBow
                break
            else
                @ignore regBow
            endif
        endwhile
    endif
    if foundHeavyXbow > 0 and not find foundHeavyXbow myBag
        lift foundHeavyXbow
        drop myBag -1 -1 -1
        #overhead 'stashing heavy xbow'
        wait 250
    endif
    
    # move spellbook
    @setvar! foundBook 0
    if findtype 'spellbook' self 2599 as book
        @setvar! foundBook book
    elseif findtype 'spellbook' self 2503 as book
        @setvar! foundBook book
    elseif findtype 'spellbook' self as book
        @setvar! foundBook book
    else 
        while findtype 'spellbook' self as book
            getlabel book bookName
            if 'blessed' in bookName
                @ignore book
            elseif 'unidentified' in bookName
                @ignore book
            elseif 'exceptional' in bookName
                @setvar! foundBook book
                break
            elseif 'magic' in bookName
                @setvar! foundBook book
                break
            else
                @ignore book
            endif
        endwhile
    endif
    if foundBook > 0 and not find foundBook myBag
        lift foundBook
        drop myBag -1 -1 -1
        #overhead 'stashing spellbook'
        wait 250
    endif
else
    overhead 'NO BAG FOUND!' 38
    stop
endif