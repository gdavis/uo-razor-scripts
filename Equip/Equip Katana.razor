# move list of weapons into safe bag
if listexists stashList
    removelist stashList
endif
createlist stashList
## Add item names using >info 
## to stash other weapon types
pushlist stashList 'kryss'
pushlist stashList 'Long Spear'
pushlist stashList 'war fork'
pushlist stashList 'short spear'
pushlist stashList 'bow'
pushlist stashList 'crossbow'
pushlist stashList 'heavy crossbow'
pushlist stashList 'spellbook'
pushlist stashList 'bardiche'
pushlist stashList 'halberd'
pushlist stashList 'viking sword'


# ----------------------------
@clearignore

# 5119 = katana
@setvar weaponID 5119

# check if equipped
if findtype weaponID righthand
    overhead '+Katana Equipped' 54
    stop
endif

# Equip Weapon
dress 'Katana'    
    
# 2207 == verite
if findtype weaponID self 2207 as weapon 
    overhead '+Verite katana' 14
# 2897 == poison aspect
elseif findtype weaponID self 2897 as weapon 
    overhead '+Poison katana' 36
# 2599 == void aspect
elseif findtype weaponID self 2599 as weapon 
    overhead '+Void katana' 14
# 2504 == war aspect
elseif findtype weaponID self 2503 as weapon
    overhead '+War katana' 36
# normal
elseif findtype weaponID self as weapon
    overhead '+Reg katana' 920
else
    overhead 'Katana not found!' 38
    stop
endif

# stash items into bag
if findtype 'bag' backpack 0 as myBag 
    @clearignore 
    foreach itemID in stashList
        while findtype itemID backpack as weapon
            getlabel weapon weaponName
            @setvar! shouldStash 0
            
            if 'unidentified' in weaponName
                sysmsg 'Ignoring magic item'
            elseif 'blessed' in weaponName
                sysmsg 'Ignoring blessed item'
            else
                @setvar! shouldStash 1
            endif
            
            if shouldStash = 1 and not find weapon myBag
                lift weapon
                drop myBag -1 -1 -1
                sysmsg 'Stashing: {{weaponName}}' 920
                wait 250
            endif
            
            @ignore weapon
        endwhile
    endfor
    @clearignore
else
    overhead 'NO BAG FOUND!' 38
    stop
endif