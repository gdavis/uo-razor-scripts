@clearignore

if findtype 'heavy crossbow' lefthand
    overhead 'Already Equipped!' 54
    stop
endif

# 2599 == void aspect
if findtype 'heavy crossbow' self 2599 as bow 
    dclick bow
    while lhandempty
    endwhile
    overhead 'Void Heavy Xbow Equipped' 14
# 2504 == war aspect
elseif findtype 'heavy crossbow' self 2503 as bow 
    dclick bow
    while lhandempty
    endwhile
    overhead 'War Heavy Xbow Equipped' 36
# normal
elseif findtype 'heavy crossbow' self as bow
    dclick bow
    while lhandempty
    endwhile
    overhead 'Heavy Xbow Equipped' 920
else
    overhead 'Heavy Xbow not found!' 38
endif

# try to move other stuff into bag
if findtype 'bag' backpack 0 as myBag 
    # move aspected bow
    @setvar! foundBow 0
    if findtype 'bow' self 2599 as aspectBow
        @setvar! foundBow aspectBow
    elseif findtype 'bow' self 2503 as aspectBow
        @setvar! foundBow aspectBow
    elseif findtype 'bow' self 1790 as aspectBow
        @setvar! foundBow aspectBow
    else 
        while findtype 'bow' self as regBow
            getlabel regBow bowName
            if 'exceptional' in bowName
                @setvar! foundBow regBow
                break
            else
                @ignore regBow
            endif
        endwhile
    endif
    if foundBow > 0 and not find foundBow myBag
        lift foundBow
        drop myBag -1 -1 -1
        #overhead 'stashing bow'
        wait 250
    endif
    
    # move aspected xbow
    @setvar! foundXbow 0
    if findtype 'crossbow' self 2599 as aspectBow
        @setvar! foundXbow aspectBow
    elseif findtype 'crossbow' self 2503 as aspectBow
        @setvar! foundXbow aspectBow
    elseif findtype 'crossbow' self 1790 as aspectBow
        @setvar! foundXbow aspectBow
    else 
        while findtype 'crossbow' self as regBow
            getlabel regBow bowName
            if 'exceptional' in bowName
                @setvar! foundXbow regBow
                break
            else
                @ignore regBow
            endif
        endwhile
    endif
    if foundXbow > 0 and not find foundXbow myBag
        lift foundXbow
        drop myBag -1 -1 -1
        #overhead 'stashing xbow'
        wait 250
    endif
    
    # move spellbook
    @setvar! foundBook 0
    if findtype 'spellbook' self 2599 as book
        @setvar! foundBook book
    elseif findtype 'spellbook' self 2503 as book
        @setvar! foundBook book
    else 
        while findtype 'spellbook' self as book
            getlabel book bookName
            if 'blessed' in bookName
                @ignore book
            elseif 'unidentified' in bookName
                @ignore book
            elseif 'exceptional' in bookName
                @setvar! foundBook book
                break
            elseif 'magic' in bookName
                @setvar! foundBook book
                break
            else
                @ignore book
            endif
        endwhile
    endif
    if foundBook > 0 and not find foundBook myBag
        lift foundBook
        drop myBag -1 -1 -1
        #overhead 'stashing spellbook'
        wait 250
    endif
else
    overhead 'NO BAG FOUND!' 38
    stop
endif