# move list of weapons into safe bag
if listexists stashList
    removelist stashList
endif
createlist stashList
## Add item names using >info 
## to stash other weapon types
pushlist stashList 'kryss'
pushlist stashList 'Long Spear'
pushlist stashList 'war fork'
pushlist stashList 'short spear'
pushlist stashList 'bow'
pushlist stashList 'crossbow'
pushlist stashList 'heavy crossbow'
pushlist stashList 'spellbook'
pushlist stashList 'bardiche'
pushlist stashList 'viking sword'
pushlist stashList 'katana'
pushlist stashList 'halberd'

# ----------------------------
@clearignore

# clear previous item moves
hotkey 'Clear Drag/Drop Queue'

if findtype 'spellbook' backpack 0 as book
    getlabel book bookName
    if 'blessed' in bookName
        @ignore book
    endif
endif

if findtype 'spellbook' self 2599 as book
    dclick book
    while not lhandempty
    endwhile
    overhead '+Void spellbook' 14
elseif findtype 'spellbook' self 2503 as book
    dclick book
    while not lhandempty
    endwhile
    overhead '+War spellbook' 36
elseif findtype 'spellbook' self as book
    dclick book
    while not lhandempty
    endwhile
    overhead '+Spellbook' 920
else
    overhead 'Spellbook not found!' 38
    stop
endif

# let item fall into bag
wait 100

# stash items into bag
if findtype 'bag' backpack 0 as myBag 
    @clearignore 
    foreach itemID in stashList
        while findtype itemID backpack as weapon
            getlabel weapon weaponName
            @setvar! shouldStash 0
            
            if 'unidentified' in weaponName
                sysmsg 'Ignoring magic item'
            elseif 'blessed' in weaponName
                sysmsg 'Ignoring blessed item'
            else
                @setvar! shouldStash 1
            endif
            
            if shouldStash = 1 and not find weapon myBag
                sysmsg 'Stashing: {{weaponName}}' 920
                lift weapon
                drop myBag -1 -1 -1
                wait 250
            endif
            
            @ignore weapon
        endwhile
    endfor
    @clearignore
else
    overhead 'NO BAG FOUND!' 38
    stop
endif