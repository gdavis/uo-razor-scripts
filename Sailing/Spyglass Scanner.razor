# Stiggr Search
# Written by Aphlux
# 
# Uses the spyglass and prints an
# alert when player ships are found.
# Should not interfere with casting.

# how often the alert text will print,
# in miliseconds.
@setvar! alertInterval 1000

# how long to wait between spyglass uses
@setvar! spyglassCooldown 6000

# when set to 1, closes the ship gump
# after using the spyglass
@setvar! closeGump 0

# You probably do not need to 
# edit below here. Probably.
#----------------------------
@setvar! shipGump 2890020940
@setvar! boardGump 2881168634

# if the boarding gump is up, 
# wait for user to target ship to board
# then we will continue
if gumpexists boardGump
    if not targetexists
        # wait for boarding target
        waitfortarget  
    endif
    # wait for target
    while targetexists 
    endwhile
endif

while gumpexists boardGump
endwhile

# if there is a target already up,
# probably casting so wait until done
while targetexists 
endwhile

clearsysmsg

# reset the ship gump
while gumpexists shipGump
    gumpclose shipGump
endwhile

# init timer
if not timerexists lastSpyglass
    createtimer lastSpyglass
    settimer lastSpyglass 99999
endif

# spyglass = 5365
if not findtype 5365 self
    overhead 'I need a spyglass!' 38
    wait 3000
    loop
endif

# try to pause when boarding
if insysmsg 'Which ship do you wish to board?'
    wft
    while targetexists 
    endwhile
    wait 500
    
# wait for save to finish
elseif insysmsg 'Saving...'
    while not insysmsg 'World save complete'
    endwhile
endif

# do the spy
overhead '*scans the horizon*' 1001 
dclicktype 5365 backpack
waitfortarget
target self
waitforgump shipGump 1000

if insysmsg 'before you make another ship search'
    wait 100
    loop
elseif insysmsg 'You must wait to perform another action'
    wait 100
    loop
endif

settimer lastSpyglass 0

# make sure we have the right gump up, or retry
if not ingump 'Search for Ships' 2890020940
    loop
endif

# select search
gumpresponse 4
waitforgump shipGump

# this is set to the ship id when found
@setvar! foundShip 0

# list of ids for different ships
@setvar! smallShip 1
@setvar! smallDragonship 2
@setvar! mediumShip 3
@setvar! mediumDragonship 4
@setvar! largeShip 5
@setvar! largeDragonship 6
@setvar! carrack 7
@setvar! galleon 8

while 1 = 1
    if ingump "a small ship" 2890020940
        @setvar! foundShip smallShip
        break
    elseif ingump "a small dragonship" 2890020940
        @setvar! foundShip smallDragonship
        break
    elseif ingump "a medium ship" 2890020940
        @setvar! foundShip mediumShip
        break
    elseif ingump "a medium dragonship" 2890020940
        @setvar! foundShip mediumDragonship
        break
    elseif ingump "a large ship" 2890020940 
        @setvar! foundShip largeShip
        break
    elseif ingump "a large dragonship" 2890020940
        @setvar! foundShip largeDragonship
        break
    elseif ingump "a carrack" 2890020940
        @setvar! foundShip carrack
        break
    elseif ingump "a galleon" 2890020940
        @setvar! foundShip galleon
        break
    elseif ingump "Next" 2890020940
        # go to next page and keep checking
        gumpresponse 3 2890020940
        wait 350
    else
        break
    endif
endwhile

if closeGump = 1
    while gumpexists shipGump
        gumpclose shipGump
    endwhile
endif

if foundShip != 0 
    for 5
        if foundShip = smallShip
            overhead 'SMALL SHIP SIGHTED!' 54
        elseif foundShip = smallDragonship
            overhead 'SMALL DRAGONSHIP SIGHTED!' 54
        elseif foundShip = mediumShip
            overhead 'SHIP SIGHTED!' 54
        elseif foundShip = dragonship
            overhead 'DRAGONSHIP SIGHTED!' 54
        elseif foundShip = largeShip
            overhead 'LARGE SHIP SIGHTED!' 54
        elseif foundShip = largeDragonship
            overhead 'LARGE DRAGONSHIP SIGHTED!' 54
        elseif foundShip = carrack
            overhead 'CARRACK SIGHTED!' 54
        elseif foundShip = galleon
            overhead 'GALLEON SIGHTED!' 54
        endif
        wait alertInterval
    endfor
endif

while timer lastSpyglass < spyglassCooldown
endwhile

loop