# Embarks your crew, followers, 
# and yourself to your boat.
#
# IMPORTANT:
# The tillerman target MUST
# be set each time your boat
# is docked and put back into
# the water.
#
# by Aphlux
# Updated June 9, 2023
sysmsg 'Embarking...' 69
clearsysmsg 
@clearignore

if gumpexists 3750376313
    gumpclose 3750376313
endif

if gumpexists 4216593270
    gumpclose 4216593270
endif

# target the tillerman and verify
if not varexist shipTillerman or not find shipTillerman ground
    overhead 'Select your boat tillerman:' 54
    setvar shipTillerman 0
    setvar! shipTillerman
endif

if shipTillerman = 0
    overhead 'Targetting cancelled!' 38
    stop
else 
    # check name to make sure the trash 
    # or something wasnt targetted
    getlabel shipTillerman tillermanName
    
    if 'Hull:' in tillermanName
    else
        overhead 'Invalid tillerman! Retarget!' 38
        unsetvar  shipTillerman
        loop
    endif
endif

# interact with tillerman
if find shipTillerman ground as tillerman
    dclick tillerman
    wait 250
    
    while not gumpexists 3750376313 and not gumpexists 4216593270
    endwhile
    
    if gumpexists 3750376313
        # this is not your boat!
        sysmsg 'Embark failed! The `shipTillerman` variable is set to a boat you cannot control.' 38
        unsetvar! shipTillerman
        gumpclose 3750376313
        loop
    elseif gumpexists 4216593270
        sysmsg 'Found my ship!' 54
    else
        sysmsg 'FATAL Error! This should never happen. You are amazing, how do you do it?!'
        stop
    endif
    
    # embark yourself
    gumpresponse 201 4216593270
    waitforgump 4216593270
    
    if insysmsg 'You are too far away to embark'
        overhead 'Too far from boat!' 38
        gumpclose 4216593270
        sysmsg 'Embark failed! Too far away from the boat!' 38
        stop
    elseif insysmsg 'Where do you wish to disembark to'
        overhead 'You are already on the boat!' 38
        hotkey 'Cancel Current Target'
        sysmsg 'Embark sorta failed! You are already onboard!'
        stop
    endif
    
    # recall boarding party
    gumpresponse 211 4216593270
    waitforgump 4216593270
    
    # embark followers
    gumpresponse 202 4216593270
    waitforgump 4216593270

    gumpclose 4216593270
    stop
endif

overhead 'EMBARK FAILED!' 38
sysmsg 'EMBARK ERROR: Could not find your ship nearby to embark, or you already on the boat. Make sure to set the tillerman target!' 38