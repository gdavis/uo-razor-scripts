# One-click boat launch that will
# embark you and your followers,
# ready the crew, reload cannons, 
# and open ship menus, and begin repairs.
# 
# Written by Aphlux
# Last updated June 26, 2023
#
# ---------------------------
# Summoner Pets
# ---------------------------
# 0 - No pet
# 1 - Earth Elemental
# 2 - Water Elemental
# 3 - Fire Elemental
# 4 - Air Elemental
# 5 - Daemon
# 6 - Creature
@setvar! pet1 1
@setvar! pet2 5
@setvar! pet3 6
# ---------------------------

clearsysmsg 
@clearignore 

if gumpexists 3287496917
    gumpclose 3287496917
endif

# Supplies
overhead 'Checking supplies...'
if findtype 'rope' self 0 as item
    getlabel item itemName
    sysmsg 'Found {{itemName}}' 908
else
    overhead 'Missing boarding ropes!' 38
endif

if findtype 'spyglass' self as item
    getlabel item itemName
    sysmsg 'Found {{itemName}}' 908
else
    overhead 'Missing spyglasses!' 38
endif

if findtype 'ship repair kit' self as item
    getlabel item itemName
    sysmsg 'Found {{itemName}}' 908
else
    overhead 'Missing repair kits!' 38
endif

if findtype 'cannon ball' self as item
    getlabel item itemName
    sysmsg 'Found {{itemName}}' 908
else
    overhead 'Missing cannon balls!' 38
endif

# this just needs 'dock' in it somewhere, can be customized
say 'dock'
waitforgump 3287496917 6000

if not gumpexists 3287496917
    overhead 'Failed to find dockmaster!' 38
    stop
endif

# select boat to launch in list
# response is 10 + button row, zero indexed.
gumpresponse 10

# wait for target cursor
waitfortarget 6000

if insysmsg 'You have traveled too recently and must wait'
    overhead 'Travel cooldown!' 38
    sysmsg 'Failed to launch! On travel cooldown!' 38
    stop
elseif not targetexists 
    overhead 'Failed to get cursor to launch ship!' 38
    stop
endif

if insysmsg 'Where do you wish to place the ship'
    overhead 'Launch ship where?'
endif

# wait for player to place ship
while targetexists 
endwhile

# timeout timer
removetimer boatLaunchTimer
createtimer boatLaunchTimer

# wait for expected sysmsgs
while 1 = 1
    if insysmsg 'A ship rune has been placed'
        overhead 'Boat in the water!'
        break
    elseif insysmsg 'You have been onboard another ship'
        overhead 'Ship not ready!' 38
        sysmsg 'Launch Ship Failed - Ship is not ready!' 38
        stop
    elseif insysmsg 'That dockmaster is too far away'
        overhead 'Dockmaster too far!' 38
        sysmsg 'Launch Ship Failed - Dockmaster is too far away!'
        stop
    elseif insysmsg 'A ship cannot be placed there'
        overhead 'Invalid location!' 38
        sysmsg 'Launch Ship Failed - Invalid launch location!' 38
        stop
    elseif insysmsg 'That location is out of your line of sight' or insysmsg 'That is too far away'
        overhead 'Location too far!' 38
        sysmsg 'Launch Ship Failed - Location out of line of sight!' 38
        stop
    elseif insysmsg 'That location is too far away' or insysmsg 'That is too far away'
        overhead 'Location too far!' 38
        sysmsg 'Launch Ship Failed - Launch location is out of range!' 38
        stop
    elseif timer boatLaunchTimer > 10000
        overhead 'Launch timed out!' 38
        sysmsg 'Launch Ship Failed - 10 seconds passed without the ship being placed!' 38
        stop
    endif
endwhile

overhead 'Boarding ship...'
say '[Embark'
wait 250

overhead 'Boarding followers...'
say '[EmbarkFollowers'
wait 250

overhead 'Waking the crew...'
say '[ReadyCrew'
wait 250

overhead 'Readying ship...'
say '[Ship'
wait 250
say '[ShipHotbars'
wait 250
say '[TargetingMode'
wait 250
say '[Reload'
wait 250

@setvar! shouldSummonPet 0
if skill 'spirit speak' >= 80
    @setvar! shouldSummonPet 1
    @setvar! petIndex 0
    
    if skill 'necromancy' > 0
        say '[VengefulSpirit'
        wait 500
        say '[Wither'
        wait 500
    endif

    while shouldSummonPet = 1 and followers < 5
        @setvar! petType 0
        
        if followers = 0
            @setvar! current 0
        elseif followers = 1
            @setvar! current 1
        elseif followers = 2
            @setvar! current 2
        elseif followers = 3
            @setvar! current 3
        elseif followers = 4
            @setvar! current 4
        endif
        
        if petIndex == 0
            @setvar! petType pet1
        elseif petIndex == 1
            @setvar! petType pet2
        elseif petIndex == 2
            @setvar! petType pet3
        endif
    
        if petType == 1
            cast 'Earth Elemental'
        elseif petType == 2
            cast 'Water Elemental'
        elseif petType == 3
            cast 'Fire Elemental'
        elseif petType == 4
            cast 'Air Elemental'
        elseif petType == 5
            cast 'Summon Daemon'
        elseif petType == 6
            cast 'Summ. Creature'
        endif
        
        if petType > 0
            # wait to finish summon
            while followers == current
            endwhile
            # wait for spell recovery
            wait 250
        endif
        
        if petIndex == 0
            @setvar! petIndex 1
        elseif petIndex == 1
            @setvar! petIndex 2
        elseif petIndex == 2
            @setvar! petIndex 3
        else
            break
        endif
    endwhile
endif

# eat foot if hungry
if not findbuff 'Food Satisfaction'
    if findtype 'tray' backpack as food 
        dclick food
        overhead 'NOM NOM NOM' 44
    else
        overhead 'HUNGRY! Need Food!' 38
    endif
else 
    overhead 'Not hungry.' 902
endif

script 'Repair Ship'


















