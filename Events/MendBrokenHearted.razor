# Auto Hearat Mending!
#
# Have a bow in your bag and a stack of mending arrows.
# Start the script and run around until you find 
# a heart tha needs mending.
#
# The script will equip, target, and shoot them.
#
# Written by Aphlux Feb 12, 2024

while 1 = 1
    if not findtype 'heart arrow' backpack
        overhead 'No more mending arrows!' 38
        stop
    endif

    if not findtype 'bow' lefthand and findtype 'bow' backpack as bow
        overhead 'Equipping heart-mending bow!' 36
        dclick bow
        wait 1000
    endif

    if not findtype 'bow' lefthand 
        sysmsg 'ERROR: Need a bow to mend hearts!' 38
        stop
    endif    

    clearsysmsg 
    hotkey 'Next Target'
    
    if targetexists 
        hotkey 'Cancel Current Target'
    endif
    
    if insysmsg 'New target set'
        @setvar! targetID lasttarget 
        getlabel lasttarget targetName
        sysmsg '{{targetName}}' 36
   
        if insysmsg 'getlabel - Skipped'
            # Skipping player
            continue
        endif  
        
        if '[Broken Hearted]' in targetName
            overhead 'Found a sad soul!'
            overhead '{{targetName}}' 36
            
            if findtype 'heart arrow' backpack as arrow
                overhead '▼▼ BROKEN HEART! ▼▼' 36 lasttarget
                dclick arrow
                wft 1000
                target targetID
                wait 250
                
                if insysmsg 'That is too far away'
                    overhead 'TOO FAR!' 38
                    continue
                endif
                
                if insysmsg 'Target cannot be seen'
                    overhead 'Cannot see!' 38
                    continue
                endif
                
                wait 2000
            else
                overhead 'Ran out of mending arrows!' 38
                stop
            endif
        endif
        
        @setvar! targetID 0
        getlabel self targetName  # clear the name so we dont get a false positive
    endif
endwhile