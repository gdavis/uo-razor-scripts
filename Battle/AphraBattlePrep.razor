overhead '+Battle Prep+' 1001
clearsysmsg 

# Start tracking
# NOTE: findbuff 'Tracking' does not yet work
if skill 'Tracking' > 50 
    overhead 'Setup tracking...' 48
    if not varexist tracking_gump_id
        setvar tracking_gump_id 4267467659
    endif

    while not gumpexists tracking_gump_id
        sysmsg 'Opening tracking gump...' 53
        skill 'tracking'
        waitforgump tracking_gump_id 600
    endwhile

    if gumpexists tracking_gump_id
        if not ingump 'Stop Hunting' 4267467659
            overhead 'Started Hunting PKs' 65
            gumpresponse 6
            wait 3000
        else
            overhead 'Already Hunting' 89
        endif
        
        gumpclose tracking_gump_id
    endif
endif

# Food
if not findbuff 'Food Satisfaction'
    if findtype 'tray' backpack as food 
        dclick food
        overhead 'NOM NOM NOM' 44
    else
        overhead 'HUNGRY! Need Food!' 38
    endif
else 
    overhead 'Not hungry.' 89
endif

# Spell buffs
if skill 'Magery' > 50
    # Reactive Armor
    if not findbuff 'Reactive Armor'
        overhead 'Need Reactive Armor' 48
        cast 'Reactive Armor'
        while not findbuff 'Reactive Armor'
        endwhile 
        wait 250
    else
        overhead 'Reactive Armor: On' 89
    endif

    # Reflect
    if not findbuff 'Magic Reflection'
        overhead 'Need Reflect' 48
        cast 'Magic Reflection'
        while not findbuff 'Magic Reflection'
            if insysmsg 'That spell will not adhere'
                overhead 'Cannot apply reflect!' 38
                break
            endif
        endwhile
        wait 250
    else
        overhead 'Magic Resist: On' 89
    endif
endif

overhead 'Ready for battle'