# Performs an initial salvo of 
# spells on a target

if targetexists 
    hotkey 'Cancel Current Target'
endif

overhead 'Salvo what?' 54
@setvar! monster

if not varexist monster
    overhead 'Must select a target.' 38
endif

if dead monster 
    overhead 'That is dead.' 36
    stop
endif

if noto monster = innocent
    overhead 'Cannot attack!' 38
    stop
elseif noto monster = hostile or noto monster = criminal or noto monster = murderer
    overhead 'Attack salvo!' 54
else 
    overhead 'Invalid target!' 38
    stop
endif

clearsysmsg 
say 'all attack'
waitfortarget 1500
target monster

@setvar! castRecoveryTime 200

@setvar! magicArrowID 101
@setvar! fireballID 102
@setvar! harmID 103
@setvar! lightningID 104
@setvar! manaDrainID 201
@setvar! curseID 202
@setvar! poisonID 203

getlabel monster monsterName
@setvar! monsterDebuffTimer monster

if not timerexists monsterDebuffTimer
    createtimer monsterDebuffTimer
    settimer monsterDebuffTimer 60000
endif

#overhead monsterDebuffTimer

removelist spellOrder
removelist spellDelay

createlist spellOrder
createlist spellDelay

# NOTE: spell delays are the circle cast time + 100 ms for latency
# https://uooutlands.com/wiki/Magery

# only include debuffs every 1m
if timer monsterDebuffTimer >= 60000
    pushlist spellOrder manaDrainID
    pushlist spellDelay 1350

    pushlist spellOrder curseID
    pushlist spellDelay 1350
else
    overhead 'Debuffs on cooldown' 87
endif

if skill 'Poisoning' > 50
    pushlist spellOrder poisonID
    pushlist spellDelay 1100
endif

pushlist spellOrder lightningID
pushlist spellDelay 1350

pushlist spellOrder fireballID
pushlist spellDelay 1100

pushlist spellOrder harmID
pushlist spellDelay 850

pushlist spellOrder magicArrowID
pushlist spellDelay 600

if poplist spellOrder back as finalSpellID
    @setvar! endSpellID finalSpellID
    pushlist spellOrder finalSpellID back
endif

while not dead monster
    #overhead 'loop'

    if hp < 35
        overhead 'LOW HEALTH!' 38 
        stop
    endif
    
    if insysmsg 'Target cannot be seen'
        overhead 'CANNOT SEE!' 38
        stop
    else 
        if poplist spellOrder front as spellID and poplist spellDelay front as delay
            if spellID = manaDrainID                
                cast 'Mana Drain'
            elseif spellID = curseID
                cast 'Curse'
            elseif spellID = fireballID
                cast 'Fireball'    
            elseif spellID = magicArrowID
                cast 'Magic Arrow'
            elseif spellID = harmID
                cast 'Harm'
            elseif spellID = lightningID
                cast 'Lightning'
            elseif spellID = poisonID
                cast 'Poison'
            else
                overhead 'Unknown spell!' 38
                stop
            endif
            
            #overhead 'delay = {{delay}}'
            wait delay
            waitfortarget 250
            
            if dead monster
                hotkey 'Cancel Current Target'
                stop
            endif
            
            target monster
            wait castRecoveryTime
            
            if insysmsg 'That is too far away'
                overhead 'TOO FAR!' 38
                stop
            elseif insysmsg 'Your concentration is distrubed' or insysmsg 'You have not yet recovered'
                # re-queue this spell and cast again
                pushlist spellOrder spellID front
                pushlist spellDelay delay front
                wait 100
            elseif spellID = endSpellID
                overhead 'SALVO FINISHED' 54
                stop
            else 
                if spellID = curseID or spellID = manaDrainID 
                    settimer monsterDebuffTimer 0
                endif
            endif
        else 
            overhead 'No spells in list!' 38
            break
        endif
    endif
    
    wait 200
endwhile