# Recalls through a series of runes
# in a runebook or tome and purchases
# a grocery list of reagents.
# Supports using packies, and best used
# when you have 2 packies. When the first
# packie is overloaded, the overloaded
# resources are placed into the second,
# and the script will then return home
# to unload the purchases. 
overhead '-Buyer Vendors-' 24

# Purchase limits before returning home
if not varexist blackPearlLimit
    @setvar! blackPearlLimit 5000
    @setvar! mandrakeLimit 5000
    @setvar! bloodmossLimit 5000
    @setvar! garlicLimit 5000
    @setvar! ginsengLimit 5000
    @setvar! sulphurousAshLimit 5000
    @setvar! nightshadeLimit 5000
    @setvar! spidersSilkLimit 5000

    @setvar! bottleLimit 5000
    @setvar! scrollLimit 5000
endif

@clearignore 

# when set to 1 and currently buying reagents,
# instead of going to the next vendor, will
# return home and unload the purchases.
setvar! shouldReturnHome 0
@setvar! firstTarget 0

if diffweight < 50
    overhead 'Carrying too much!' 38
    sysmsg 'Carrying too much weight, puchases stopped.'
    
    overhead 'Returning to home...'
    script 'Reagent Buyer Return Home'                        
    stop
endif


@setvar! reachedLimit 0

removelist satchelRegs
createlist satchelRegs

while 1 = 1
    clearsysmsg 
    hotkey 'Next Target'
    
    if insysmsg 'New target set'
        @setvar! targetID lasttarget 
        getlabel lasttarget targetName
        #overhead '{{targetName}}'
   
        if insysmsg 'getlabel - Skipped'
            # Skipping player
            continue
        endif  
        
        if 'the mage' in targetName or 'the alchemist' in targetName
            overhead 'Found a vendor!' 54
        
            # buy what we can
            menu targetID 1
            wait 500
            
            # move purchased items to the packie
            # 3976|3981|3980|3973|3974|3962|3972|3963 = regs
            # 3854 = empty bottles
            # 3827 = scrolls
            sysmsg 'Looking for reagents to move the packie...' 54
            
            removelist foundItems
            createlist foundItems
            
            # find items on self that are not in our satchel
            @setvar! satchelID 0
            if findtype 'reagent satchel' self as satchel
                @setvar! satchelID satchel
            endif
           
            while findtype 3976|3981|3980|3973|3974|3962|3972|3963|3854|3827 self as item
                @setvar! isValidItem 0
                if satchelID = 0
                    @setvar! isValidItem 1
                else 
                    if inlist satchelRegs item 
                        # do nothing, e.g. already ignored
                    elseif find item satchelID
                        # add new satchel reg to invalid list
                        pushlist satchelRegs item
                    else 
                        # valid when not in satchel
                        @setvar! isValidItem 1
                    endif
                endif
                
                if isValidItem = 1
                    getlabel item itemName
                    sysmsg 'Found: {{itemName}}' 69
                    pushlist foundItems item back
                else
                    overhead 'invalid item'
                endif
                
                @ignore item
            endwhile
            
            # pickup items on ground
            while findtype 3976|3981|3980|3973|3974|3962|3972|3963|3854|3827 ground any any 2 as item
                if not inlist satchelRegs item
                    getlabel item itemName
                    pushlist foundItems item back
                    sysmsg 'Found: {{itemName}}'
                endif
            
                @ignore item
            endwhile
            
            # move each found item
            foreach item in foundItems
                getlabel item itemName
                overhead 'Moving {{itemName}}' 54
                
                # move purchased items to packie
                if followers > 0
                    @setvar! skipItem 0
                    while findtype 'a pack llama' ground any any 2 as packie and skipItem = 0
                        if noto packie = friend
                            if find item packie
                                @ignore item
                                @setvar! skipItem 1
                                sysmsg 'item is in packie, ignoring.' 38
                                continue
                            endif
                            
                            clearsysmsg
                            lift item 60000
                            drop packie -1 -1 -1
                            wait 666
                            @setvar! skipItem 1
                            @ignore item
                            
                            if counttype item packie > 0
                                overhead 'we have some item!'
                            endif
                            
                            if insysmsg 'You are overloaded.'
                                overhead 'Packie is full!' 38
                                setvar! shouldReturnHome 1
                                @ignore packie
                            else
                                overhead 'Finished moving items.'
                            endif
                            
                            # Reagent Limits
                            dclick packie
                            wait 150
                            
                            if blackPearlLimit > 0 and counttype 3962 packie > blackPearlLimit
                                overhead 'Reached Black Pearl limit' 54
                                @setvar! reachedLimit 1
                            endif
                            if mandrakeLimit > 0 and counttype 3974 packie > mandrakeLimit
                                overhead 'Reached Mandrake limit' 54
                                @setvar! reachedLimit 1
                            endif
                            if bloodmossLimit > 0 and counttype 3963 packie > bloodmossLimit
                                overhead 'Reached Bloodmoss limit' 54
                                @setvar! reachedLimit 1
                            endif
                            if garlicLimit > 0 and counttype 3972 packie > garlicLimit
                                overhead 'Reached Garlic limit' 54
                                @setvar! reachedLimit 1
                            endif
                            if ginsengLimit > 0 and counttype 3973 packie > ginsengLimit
                                overhead 'Reached Ginseng limit' 54
                                @setvar! reachedLimit 1
                            endif
                            if sulphurousAshLimit > 0 and counttype 3980 packie > sulphurousAshLimit
                                overhead 'Reached Sulphuous Ash limit' 54
                                @setvar! reachedLimit 1
                            endif
                            if nightshadeLimit > 0 and counttype 3976 packie > nightshadeLimit
                                overhead 'Reached Night Shade limit' 54
                                @setvar! reachedLimit 1
                            endif
                            if spidersSilkLimit > 0 and counttype 3981 packie > spidersSilkLimit
                                overhead 'Reached Spider Silk limit' 54
                                @setvar! reachedLimit 1
                            endif
                            
                            # Misc Limits
                            if bottleLimit > 0 and counttype 3854 packie > bottleLimit
                                overhead 'Reached Empty Bottle limit' 54
                                @setvar! reachedLimit 1
                            endif
                            if scrollLimit > 0 and counttype 3827 packie > scrollLimit
                                overhead 'Reached Blank Scroll limit' 54
                                @setvar! reachedLimit 1
                            endif
                        else 
                            @ignore packie
                        endif
                    endwhile
                endif
            endfor
            
            if reachedLimit = 1
                setvar! shouldReturnHome 1
            endif

            # set the intial target if needed
            if firstTarget == 0
                @setvar! firstTarget targetID 
                overhead 'FIRST TARGET = {{firstTarget}}'
            elseif firstTarget == targetID 
                overhead 'Location complete' 89
            
                if isBuyingReagents = 1
                    if shouldReturnHome = 1
                        overhead 'Returning to home...'
                        script 'Reagent Buyer Return Home'
                    else
                        overhead 'Recalling to next location...'
                        script 'Reagent Buyer Recall'
                    endif
                else 
                    break
                endif
            endif  
        endif
    endif
endwhile
@clearignore 