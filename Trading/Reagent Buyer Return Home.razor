# Recalls to runebook, 
# then enters an inn room.
# Modified for the resource buyer script routines.
# 
# Written by:       Aphlux (aka Aphra)
# Return to Inn room
overhead '-Buyer Return Home-' 24
if not varexist recallInnTarget
    overhead 'Select recall target:'
    setvar recallInnTarget
endif    

hotkey 'Cancel Current Target'

cast 'Recall'
wft 6000
target recallInnTarget

while not position 4293 812 and not position 4179 1829
    overhead 'Waiting to arrive...' 902
    wait 250
endwhile

clearsysmsg 
@setvar rentalGumpID 2393832411
@setvar! didEnterRoom 0

if gumpexists rentalGumpID
    gumpclose
    wait 100
endif

say 'room'
waitforgump rentalGumpID 3000

if not gumpexists rentalGumpID
    overhead 'Failed to enter room!' 38
    overhead 'Are you even near the inn?' 53
elseif ingump 'Enter Your Room'
    wait 250

    while not insysmsg 'You enter the rental room'
        overhead 'Entering room...' 903
        gumpresponse 4
        wait 250
        
        if insysmsg 'That vendor is too far away'
            overhead 'Innkeeper is too far away!' 38
            overhead 'Move closer!' 53
            stop
        endif
    endwhile
    
    @setvar! didEnterRoom 1
    overhead 'Welcome home.'
    
elseif ingump 'Rent This Room'
    gumpresponse 5
    waitforgump rentalGumpID 3000
    
    if ingump 'co-owner'
        while not insysmsg 'You enter the rental room'
            wait 250
            overhead 'Entering co-owned room...' 903
            gumpresponse 100
            
            if insysmsg 'That vendor is too far away'
                overhead 'Innkeeper is too far away!' 38
                stop
            endif
        endwhile
        
        @setvar! didEnterRoom 1
        overhead 'Welcome home.'
    else
        overhead 'No access to any rooms!' 38
    endif
elseif ingump 'Visit Other Rooms'
    gumpresponse 2
    waitforgump rentalGumpID 3000
    
    while not insysmsg 'You enter the rental room'
        wait 250
        overhead 'Entering co-owned room...' 903
        gumpresponse 100
    endwhile
    
    @setvar! didEnterRoom 1
    overhead 'Welcome home.'
else 
    sysmsg 'Character does not have a room or access to one!' 38
    overhead 'No room access!' 38
endif

if didEnterRoom = 1
    script 'Reagent Buyer Unload'
endif