# Copy of Distillobot for NEW! GH.
# Distillo-bot 3000
#
# Automatically creates new distillations when 
# extracts are placed into a selected container.
# The new distillation will be returned
# to the same container.
#
# Designed for use by guilds or homeowners who
# want to run a bot that will create distillations
# for other players.
#
# Requirements:
# A container within reach used as a drop box for extracts.
# Cannot be a bag in the crafters backpack.
#
# The character running this script should have a stock
# of empty bottles and mortal and pestles in their backpack.
#
# Restock commands can be added to restock from another container.
#
# Written by:       Aphlux (aka Aphra)
# Created on:       September 4, 2022
# Last updated:     January 22, 2023
#overhead '--DISTILLATIONS--' 24
clearsysmsg

# gump vars
@setvar! alchGumpID 949095101
@setvar! aspectPageID 108
@setvar! nextPageResponse 11

if not varexist distillRetunToBuff
    @setvar distillRetunToBuff 1
elseif distillRetunToBuff = 1
    overhead 'Returning to Buffing' 54
    @setvar distillRetunToBuff 0
    script 'NEW BuffBot'
else
    #overhead 'Checking for extracts...' 44
    @setvar distillRetunToBuff 1
endif

# set to 1 to also run a buff-bot script.
@setvar! buffScript 1
@setvar! distillFinished 0

# select the drop bag where people will place extracts
if not varexist distDrop
    overhead 'Select the bag to watch for extracts in.'
    setvar distDrop

    if find distDrop self
        overhead 'INVALID: The drop bag cannot be in your own pockets!' 38
        wait 1000
        unsetvar distDrop
        loop
    endif
    overhead 'NEW! Distillbot Initialized.'
else
    dclick distDrop
endif

overhead 'Checking extract dropbox...' 920
wait 250

# pickup bottles on the floor
if counttype 'empty bottle%s%' self = 0
    @setvar bottleID 0
    if findtype 'empty bottle%s%' ground -1 -1 2 as bottles
        @setvar bottleID bottles 
    elseif findtype 'empty bottle%s%' distDrop as bottles
        @setvar bottleID bottles 
    endif
    
    if bottleID > 0
        lift bottleID 60000
        drop backpack -1 -1 -1
        say 'Thank you for the bottles!'
        wait 1000
    endif
endif

# pickup mortal and pestle on the floor
if counttype 'mortar and pestle' self = 0 
    @setvar! toolID 0
    if findtype 'mortar and pestle' ground -1 -1 2 as tool
        @setvar! toolID tool
    elseif findtype 'mortar and pestle' distDrop as tool
        @setvar! toolID tool
    endif
    
    if toolID > 0
        lift toolID 60000
        drop backpack -1 -1 -1
        say 'Thank you for the tools!'
        wait 1000
   endif
endif

if not findtype 'mortar and pestle' self
    overhead 'Need more mortal and pestles!' 38
    say "I am out of mortar and pestles! Please drop some on the floor for me."
    wait 3000
    loop
endif

@setvar! foundExtract 0
@setvar! shouldSpeak 1 

# check for an extract in the drop bag and
# move the extract from drop box
while findtype 'bottle' distDrop as extract
    if insysmsg 'The world will save in 15'
        say 'One moment while I wait for the world save...'
        wait 15000
        clearsysmsg 
    elseif insysmsg 'The world is saving'
        wait 6000
    endif

    overhead 'Found an extract! Creating a new distill...'
    
    if shouldSpeak = 1
        @setvar! shouldSpeak 0
        say 'Ah! Let me distill that for you.'
        wait 500
    endif

    # check for empty bottles
    if not findtype 'empty bottle%s%' self
        overhead 'Need more empty bottles!' 38
        say "I am out of empty bottles! Please drop some on the floor for me."
        wait 3000
        loop
    endif

    # move the extract once we know we can make a distill
    lift extract 99
    drop backpack -1 -1 -1
    wait 650
endwhile

# make distills from what is in the backpack
while findtype 'bottle' self as extract
    # check for bottles during the loop
    if counttype 'empty bottle%s%' self = 0
        say 'I am out of bottles! Please drop some on the ground or in the distill box'
        wait 2500
        loop
    endif    

    # open the alch gump, and go to page 2
    # of the aspect section where distills begin
    if findtype 'mortar and pestle' self as tool
        gumpclose alchGumpID

        dclick tool
        waitforgump alchGumpID
        
        # goto 'Aspect Items'
        gumpresponse aspectPageID alchGumpID
        waitforgump alchGumpID
        
        # goto page 2
        gumpresponse nextPageResponse alchGumpID
        waitforgump alchGumpID
        wait 650
        
        # goto page 3
        gumpresponse nextPageResponse alchGumpID
        waitforgump alchGumpID
        wait 650
    else
        overhead 'Need more mortal and pestles!' 38
        say "I am out of mortar and pestles! Please drop some on the floor for me."
        wait 3000
        loop
    endif    

    getlabel extract extractName

    # go to the page for this extract,
    # then create it
    // page 3
    if 'Air' in extractName
        overhead 'Air' 1001
        gumpresponse 200 alchGumpID

    elseif 'Artisan' in extractName
        overhead 'Artisan' 544
        gumpresponse 201 alchGumpID

    elseif 'Blood' in extractName
        overhead 'Blood' 38        
        gumpresponse nextPageResponse alchGumpID
        waitforgump alchGumpID
        gumpresponse 202 alchGumpID
        
    elseif 'Command' in extractName
        overhead 'Command' 68
        gumpresponse nextPageResponse alchGumpID
        waitforgump alchGumpID
        gumpresponse 203 alchGumpID

    elseif 'Death' in extractName
        overhead 'Death' 923
        gumpresponse nextPageResponse alchGumpID
        waitforgump alchGumpID
        gumpresponse 204 alchGumpID

    elseif 'Discipline' in extractName
        overhead 'Discipline' 442
        gumpresponse nextPageResponse alchGumpID
        waitforgump alchGumpID
        gumpresponse 205 alchGumpID

    elseif 'Earth' in extractName
        overhead 'Earth' 544
        gumpresponse nextPageResponse alchGumpID
        waitforgump alchGumpID
        gumpresponse 206 alchGumpID

    elseif 'Eldritch' in extractName
        overhead 'Eldritch' 24
        gumpresponse nextPageResponse alchGumpID
        waitforgump alchGumpID
        gumpresponse 207 alchGumpID

    elseif 'Fire' in extractName
        overhead 'Fire' 38
        gumpresponse nextPageResponse alchGumpID
        waitforgump alchGumpID
        gumpresponse 208 alchGumpID

    elseif 'Fortune' in extractName
        overhead 'Fortune' 53
        gumpresponse nextPageResponse alchGumpID
        waitforgump alchGumpID
        gumpresponse 209 alchGumpID
    
    // page 4
    elseif 'Gadget' in extractName
        overhead 'Gadget' 53
        gumpresponse nextPageResponse alchGumpID
        waitforgump alchGumpID
        gumpresponse 200 alchGumpID
        
    elseif 'Harvest' in extractName
        overhead 'Harvest' 53
        gumpresponse nextPageResponse alchGumpID
        waitforgump alchGumpID
        gumpresponse 201 alchGumpID

    elseif 'Holy' in extractName
        overhead 'Holy' 53
        gumpresponse nextPageResponse alchGumpID
        waitforgump alchGumpID
        gumpresponse 202 alchGumpID

    elseif 'Lyric' in extractName
        overhead 'Lyric' 38
        gumpresponse nextPageResponse alchGumpID
        waitforgump alchGumpID
        gumpresponse 203 alchGumpID

    elseif 'Poison' in extractName
        overhead 'Poison' 68
        gumpresponse nextPageResponse alchGumpID
        waitforgump alchGumpID
        gumpresponse nextPageResponse alchGumpID
        waitforgump alchGumpID
        gumpresponse 204 alchGumpID

    elseif 'Shadow' in extractName
        overhead 'Shadow' 923
        gumpresponse nextPageResponse
        waitforgump alchGumpID
        gumpresponse nextPageResponse
        waitforgump alchGumpID
        gumpresponse 205 alchGumpID

    elseif 'Void' in extractName
        overhead 'Void' 14
        gumpresponse nextPageResponse alchGumpID
        waitforgump alchGumpID
        gumpresponse nextPageResponse alchGumpID
        waitforgump alchGumpID
        gumpresponse 206 alchGumpID

    elseif 'War' in extractName
        overhead 'War' 44
        gumpresponse nextPageResponse alchGumpID
        waitforgump alchGumpID
        gumpresponse nextPageResponse alchGumpID
        waitforgump alchGumpID
        gumpresponse 207 alchGumpID
        
    elseif 'Water' in extractName
        overhead 'Water' 8
        gumpresponse nextPageResponse alchGumpID
        waitforgump alchGumpID
        gumpresponse nextPageResponse alchGumpID
        waitforgump alchGumpID
        gumpresponse 208 alchGumpID

    else 
        overhead 'Unknown extract type! {{extractName}}' 38
        say 'DISTILLO-BOT FATAL ERROR! CANNOT COMPUTE! CANNOT COMPUTE!'
    endif

    waitforgump alchGumpID 1000
    gumpclose alchGumpID
    wait 3000
endwhile

# move the new distillations back into the drop bag
while findtype 'flask' self as distillation
    getlabel distillation distillName
    overhead 'moving {{distillName}} to drop box'
    lift distillation 99
    drop distDrop -1 -1 -1
    wait 1500
    @setvar! foundExtract 1
endwhile

if foundExtract == 1
    gumpclose alchGumpID
    clearsysmsg 
    random 5
    if insysmsg 'Random: 1'
        say 'Aaannnd done. Stuffs in the thing.'
    elseif insysmsg 'Random: 2'
        say 'Wow, I did that in record time. Was it good for you too?'
    elseif insysmsg 'Random: 3'
        say 'All done. Are you really gonna drink that..?'
    elseif insysmsg 'Random: 4'
        say 'Cool thanks many enjoy extract wow joe wow.!1^ beep boop beep'
    elseif insysmsg 'Random: 5'
        say 'Please leave a five star review if you enjoyed this service!'
    endif    
endif

loop