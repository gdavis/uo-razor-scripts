# NEW! Loot Dropoff
# Moves distributed items into 3 chests:
# - regular items (gold check chest)
# - lessers (plant chems, arcane scrolls, <6k items)
# - greaters (cores, maps, nice stuff)
#
# TIP: If you target the wrong container,
# retarget the correct one using the Options
# tab above and update the 'lootBag' variable
#
# Version 1.0
# Last updated July 15, 2024
# by Aphlux
#----------------------------------------------------------- 
# 

# set this to 0 if you are placing runes
@setvar! dropOffRunes 1

# Timings
@setvar! dragDelay 100
@setvar! dropDelay 300

# setup loot container
if overrideLootbag != 0
    setvar lootBag overrideLootbag
elseif not varexist lootBag or not find lootBag self
    overhead 'Select your loot container or target yourself:' 53
    setvar lootBag 0
    setvar lootBag
endif

if lootBag = 0
    overhead 'Failed to find loot bag!' 38
    sysmsg 'Failed to find the loot bag! Restart the script and retarget the loot bag.'
    stop
endif


# Dump magic items and runes
# NOTE: this is done before checking the chests
# since the script will stop if they are not in range.

if findtype 'magic item recycler' ground any any 2 as recycler
    overhead 'Dropped off magic items' 68
    menu recycler 1
endif

if dropOffRunes = 1 and findtype 29104 ground 2085 any 2 as tome
    overhead 'Dropped off runes' 68
    menu tome 0
endif

# Prep chests ---------------------------

@setvar! goldChest 0x4D70B4E2
@setvar! lessersChest 0x41E85C8E
@setvar! greatersChest 0x4981ED36

if not find goldChest ground any any 2 
    overhead 'Cannot reach gold chest!' 38
    stop
endif

if not find lessersChest ground any any 2 
    overhead 'Cannot reach lessers chest!' 38
    stop
endif

if not find greatersChest ground any any 2 
    overhead 'Cannot reach greaters chest!' 38
    stop
endif

dclick goldChest
waitforgump 2865667423

dclick lessersChest
waitforgump 2865667423

dclick greatersChest
waitforgump 2865667423

# Loot Lists -----------------------------

removelist regulars
removelist lessers
removelist lessersStacked
removelist greaters

createlist regulars
createlist lessers
createlist lessersStacked
createlist greaters

# regular items --------------------
pushlist regulars 'gold coin'
pushlist regulars 'research materials'
pushlist regulars 'seed of renewal'
pushlist regulars 'toy'
pushlist regulars 'boss token'
pushlist regulars 'statue'

# lesser rares ---------------------

# plant chemicals
pushlist lessersStacked 29030

# arcane essence
pushlist lessersStacked 12686

# carpet dye
pushlist lessers 29036

# collectible cards
pushlist lessers 'parchment'

# accessory dye
pushlist lessers 3843 

# all aspect distillations
pushlist lessers 17686

# aspect phylactaries
pushlist lessers 'phylactery'

# greater rares --------------------

# chromatics
pushlist greaters 'chroma core'
pushlist greaters 'chroma distil'

# aspect extract
pushlist greaters 3836

# mastercrafting diagram
pushlist greaters 17087

# rare cloth
pushlist greaters 5981              

# skill ball
pushlist greaters 22336             

# spell hue deed
pushlist greaters 5359  
pushlist greaters 11858           

# backpack dye
pushlist greaters 3839              

# facial hair dye
pushlist greaters 3843              

# funiture dye
pushlist greaters 29025   
          
# hair dye
pushlist greaters 3843              

# headwear dye
pushlist greaters 3842              

# runebook dye
pushlist greaters 3838              

# tattoo dye
pushlist greaters 15297

# mount gear dye
pushlist greaters 15296 

# mastery chain dyes
pushlist greaters 29034

# melloweed
pushlist greaters 51094

pushlist greaters 'dungeon seed'
pushlist greaters 'carpet'
pushlist greaters 'rug'
pushlist greaters 'immaculate stone'
pushlist greaters 'pristine lumber ' 

# wildlands
pushlist greaters 'dig tools'
pushlist greaters 'manuscript pages'
pushlist greaters 'redline bottle'


# Complex rare rules ----------------
# e.g. resource maps vs treasure maps
# Anything worth less than 6k goes
# into the  lesser rares chest
@clearignore 

# aspect cores
while findtype 'aspect core' lootBag as loot
    getlabel loot lootName
    sysmsg 'Moving aspect core: {{lootName}}' 442
    lift loot 1
    wait dragDelay
    
    # lower value cores for lessers
    if 'blood' in lootName or 'death' in lootName or 'discipline' in lootName or 'earth' in lootName or 'fire' in lootName or 'holy' in lootName or 'water' in lootName
       @setvar! dropChest lessersChest
    
    # higher value cores for greater 
    else
        @setvar! dropChest greatersChest
    endif
    
    # random horizontal placement at top
    clearsysmsg 
    random 10
    if insysmsg 'Random: 1'
        drop dropChest 0 0 0
    elseif insysmsg 'Random: 2'
        drop dropChest 28 0 0
    elseif insysmsg 'Random: 3'
        drop dropChest 40 0 0
    elseif insysmsg 'Random: 4'
        drop dropChest 52 0 0
    elseif insysmsg 'Random: 5'
        drop dropChest 64 0 0
    elseif insysmsg 'Random: 6'
        drop dropChest 76 0 0
    elseif insysmsg 'Random: 7'
        drop dropChest 88 0 0
    elseif insysmsg 'Random: 8'
        drop dropChest 100 0 0
    elseif insysmsg 'Random: 9'
        drop dropChest 112 0 0
    elseif insysmsg 'Random: 10'
        drop dropChest 124 0 0
    endif
    
    wait dropDelay
endwhile

# treasure & resource maps
while findtype 'map' lootBag as loot
    getlabel loot lootName
    sysmsg 'Moving map: {{lootName}}' 442
    lift loot 1
    wait dragDelay
    
    @setvar! dropChest greatersChest
    @setvar! dropY 145
    
    # standard treasure map
    if hue loot = 0
        
    # skinning map
    elseif hue loot = 2651
        @setvar! dropY 140
        if 'avarhide' in lootName or 'valehide' in lootName  or 'verehide' in lootName or 'rosehide' in lootName
        else
            @setvar! dropChest lessersChest
        endif
    
    # ore map 
    elseif hue loot = 2796
        @setvar! dropY 135
        if 'dull' in lootName or 'shadow' in lootName or 'copper' in lootName or 'bronze' in lootName
            @setvar! dropChest lessersChest
        endif
        
    # lumber map
    elseif hue loot = 2799
        @setvar! dropY 130
        if 'dull' in lootName or 'shadow' in lootName or 'copper' in lootName or 'bronze' in lootName
            @setvar! dropChest lessersChest
        endif
    
    # fishing map
    elseif hue loot = 2904
        @setvar! dropY 125
    
        if 'avarscale' in lootName
        else
            @setvar! dropChest lessersChest
        endif
    endif
    
    if 'avar' in lootName or 'level 7' in lootName
        @setvar! dropX 0
    elseif 'vale' in lootName or 'valor' in lootName or 'level 6' in lootName
        @setvar! dropX 15
    elseif 'vere' in lootName or 'level 5' in lootName
        @setvar! dropX 30
    elseif 'agapite' in lootName or 'rose' in lootName or 'level 4' in lootName
        @setvar! dropX 45
    elseif 'gold' in lootName or 'level 3' in lootName
        @setvar! dropX 60
    elseif 'bronze' in lootName or 'level 2' in lootName
        @setvar! dropX 75
    elseif 'dull' in lootName or 'level 1' in lootName
        @setvar! dropX 90
    elseif 'copper' in lootName
        @setvar! dropX 105
    elseif 'shadow' in lootName
        @setvar! dropX 120
    endif
    
    drop dropChest dropX dropY -1
    wait dropDelay
endwhile

# skill scrolls
while findtype 'scroll of calling' lootBag as loot
    getlabel loot lootName
    sysmsg 'Moving skill scroll: {{lootName}}' 89
    lift loot 1
    wait dragDelay
    
    # greaters
    if 'alchemy' in lootName or 'animal lore' in lootName or 'animal taming' in lootName or 'chivalry' in lootName or 'detect hidden' in lootName or 'herding' in lootName or 'inscription' in lootName or 'lockpicking' in lootName or 'item id' in lootName or 'necromancy' in lootName or 'stealth' in lootName or 'taste' in lootName or 'veterinary' in lootName or 'tailoring' in lootName
        @setvar! dropChest greatersChest
    
    # lessers
    else
        @setvar! dropChest lessersChest
    endif
    
    # random horizontal placement at top
    clearsysmsg 
    random 10
    if insysmsg 'Random: 1'
        drop dropChest 0 0 0
    elseif insysmsg 'Random: 2'
        drop dropChest 28 0 0
    elseif insysmsg 'Random: 3'
        drop dropChest 40 0 0
    elseif insysmsg 'Random: 4'
        drop dropChest 52 0 0
    elseif insysmsg 'Random: 5'
        drop dropChest 64 0 0
    elseif insysmsg 'Random: 6'
        drop dropChest 76 0 0
    elseif insysmsg 'Random: 7'
        drop dropChest 88 0 0
    elseif insysmsg 'Random: 8'
        drop dropChest 100 0 0
    elseif insysmsg 'Random: 9'
        drop dropChest 112 0 0
    elseif insysmsg 'Random: 10'
        drop dropChest 124 0 0
    endif
    
    wait dropDelay
endwhile

# arcane scrolls (because requires hue)
while findtype 'blank scroll%s%' lootBag 2609 as loot
    getlabel loot lootName
    sysmsg 'Moving arcane scroll: {{lootName}}' 89
    lift loot 60000
    wait dragDelay
    drop lessersChest -1 -1 -1
    wait dropDelay
endwhile

# dump all non-regular leather
while findtype 'cut up leather' lootBag as loot
    if hue loot = 0
        @ignore loot
    else
        lift loot 60000
        wait dragDelay
        drop goldChest -1 -1 -1
        wait dropDelay
    endif
endwhile

# Sort items to chests ---------------------------

foreach itemID in regulars
    while findtype itemID lootBag as loot
        getlabel loot lootName
        sysmsg 'Moving regular item: {{lootName}}' 1001
        lift loot 60000
        wait dragDelay
        drop goldChest -1 -1 -1
        wait dropDelay
    endwhile
endfor

# move lessers that should be separated into single items
foreach itemID in lessers
    while findtype itemID lootBag as loot
        getlabel loot lootName
        sysmsg 'Moving lesser rare: {{lootName}}' 89
        lift loot 1
        wait dragDelay
        
        # random horizontal placement at top
        clearsysmsg 
        random 10
        if insysmsg 'Random: 1'
            drop lessersChest 0 125 0
        elseif insysmsg 'Random: 2'
            drop lessersChest 28 125 0
        elseif insysmsg 'Random: 3'
            drop lessersChest 40 125 0
        elseif insysmsg 'Random: 4'
            drop lessersChest 52 125 0
        elseif insysmsg 'Random: 5'
            drop lessersChest 64 125 0
        elseif insysmsg 'Random: 6'
            drop lessersChest 76 125 0
        elseif insysmsg 'Random: 7'
            drop lessersChest 88 125 0
        elseif insysmsg 'Random: 8'
            drop lessersChest 100 125 0
        elseif insysmsg 'Random: 9'
            drop lessersChest 112 125 0
        elseif insysmsg 'Random: 10'
            drop lessersChest 124 125 0
        endif
        
        wait dropDelay
    endwhile
endfor

# move lessers that can be stacked
foreach itemID in lessersStacked
    while findtype itemID lootBag as loot
        getlabel loot lootName
        sysmsg 'Moving stacked lesser rare: {{lootName}}' 89
        lift loot 60000
        wait dragDelay
        drop lessersChest -1 -1 -1
        wait dropDelay
    endwhile
endfor

# move greaters separated into single item stacks
foreach itemID in greaters
    while findtype itemID lootBag as loot
        getlabel loot lootName
        sysmsg 'Moving greater rare: {{lootName}}' 14
        lift loot 1
        wait dragDelay
        
        # random horizontal placement at top
        clearsysmsg 
        random 10
        if insysmsg 'Random: 1'
            drop greatersChest 0 0 0
        elseif insysmsg 'Random: 2'
            drop greatersChest 28 0 0
        elseif insysmsg 'Random: 3'
            drop greatersChest 40 0 0
        elseif insysmsg 'Random: 4'
            drop greatersChest 52 0 0
        elseif insysmsg 'Random: 5'
            drop greatersChest 64 0 0
        elseif insysmsg 'Random: 6'
            drop greatersChest 76 0 0
        elseif insysmsg 'Random: 7'
            drop greatersChest 88 0 0
        elseif insysmsg 'Random: 8'
            drop greatersChest 100 0 0
        elseif insysmsg 'Random: 9'
            drop greatersChest 112 0 0
        elseif insysmsg 'Random: 10'
            drop greatersChest 124 0 0
        endif
        
        wait dropDelay
    endwhile
endfor

# reset override
@setvar overrideLootbag 0

overhead 'Drop off done!' 68
sysmsg 'Drop off done!' 68