# Script for Chiv bot in my inn.
# Opens a gate to town, dungeons, or shrines
# when a matching light source is lit.
# Will walk forward out of a chair when
# opening so you can walk right in.
#overhead '--GATE BOT--' 920

# running overhead timer
if not timerexists gateRunning
    createtimer gateRunning
    settimer gateRunning 99999
endif

# timer to open gates every 30s
@setvar! gateCooldown 32500
if not timerexists gateTimer
    createtimer gateTimer
    settimer gateTimer 99999
endif

# timer to only announce waiting
@setvar gateChatterDelay 10000
if not timerexists gateChatTimer
    createtimer gateChatTimer
    settimer gateChatTimer 99999
endif

# state properties
@setvar! destination 0
@setvar! itemOn 0
@setvar! itemOff 0
@setvar! itemHue 0

# locations that match chiv gate
# gump button ids
@setvar! townsLoc 1
@setvar! dungeonLoc 2
@setvar! shrinesLoc 3

# running
if timer gateRunning > 15000
    overhead 'Waiting to gate...' 1067
    settimer gateRunning 0
endif

# maus lantern for towns
@setvar! lanternOn 2594
@setvar! lanternOff 2597

if findtype lanternOn ground 2092 any 2 as lantern
    @setvar! destination townsLoc
    @setvar! itemOn lanternOn
    @setvar! itemOff lanternOff
    @setvar! itemHue 2092
    overhead 'Town Gate' 54
    say 'Town portal, coming up!'
endif

# green lantern for towns
# 1182 = wilderness hue
@setvar! torchOn 2594
@setvar! torchOff 2597
if findtype torchOn ground 1182 any 2 as torch
    @setvar! destination dungeonLoc
    @setvar! itemOn torchOn
    @setvar! itemOff torchOff
    @setvar! itemHue 1182
    overhead 'Dungeon Gate' 54
    say 'Dungeon gate, right away!'
endif

# golden oil lamp for shrines
@setvar! candleOn 55434
@setvar! candleOff 55433
if findtype candleOn ground any any 2 as candle
    @setvar! destination shrinesLoc
    @setvar! itemOn candleOn
    @setvar! itemOff candleOff
    overhead 'Shrine Gate'
    say 'Shrine gate, on it!'
endif

if destination = 0 
    script 'NEW BuffBot'
endif

if gumpexists 1482653814
    gumpclose 1482653814
endif

# wait for cooldown if needed
while timer gateTimer < gateCooldown
    # announce the delay
    if timer gateChatTimer > gateChatterDelay
        if destination == townsLoc 
            say 'Town portal up next...'
        elseif destination == dungeonLoc
            say 'Dungeons up next...'
        elseif destination == shrinesLoc
            say 'Shrines up next...'
        endif
        
        settimer gateChatTimer 0
    endif
    
    # check for the item being turned off
    # and abort the gate.
    if findtype itemOff ground itemHue any 2 as item
        say 'Okay, nevermind the portal.'
        loop
    endif
    wait 1000
endwhile

# gate to destination
say '[SacredJourney'
waitforgump 1482653814 6000
gumpresponse destination
settimer gateTimer 0
say 'Safe travels!'

# turn off item
if itemOn != 0 and findtype itemOn ground itemHue any 2 as item
    overhead 'Turning off item.' 920
    dclick item
    wait 1000
endif

script 'NEW BuffBot'