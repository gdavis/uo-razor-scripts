# Dungeon Escape by Aphlux
# Searches for a dungeon or player
# gate for 5 seconds and uses it.
# Defaults to Andaria for towns.
clearsysmsg
overhead 'GTFO' 54

if not timerexists gtfoTimer
    createtimer gtfoTimer
endif
settimer gtfoTimer 0
@setvar! gtfoTimeout 5000

while 1 = 1
    clearsysmsg 

    # Player gate, moongate, dungeon gate
    if findtype 3948 'ground' any -1 2 as gate
        overhead 'Moongate Exit' 53
        dclick gate
        # Player gate
        if hue gate = 0
            waitforgump 3899019871 800
            gumpresponse 2 3899019871
            wait 200
            
            if insysmsg 'too far away'
                continue
            endif
            
            stop
        # Moongate, dungeon gate
        else
            waitforgump 3773199800 800
            # Select Andaria
            gumpresponse 11
            waitforgump 3773199800 800
            gumpresponse 2 3773199800            
            wait 200
            
            if insysmsg 'too far away'
                continue
            endif
            
            stop
        endif
        
    # player chiv gate
    elseif findtype 30528 'ground' 2091 -1 2 as gate
        overhead 'Chiv Exit' 53
        dclick gate
        waitforgump 3773199800
        gumpresponse 2 3773199800
        
        if insysmsg 'too far away'
            continue
        endif
            
        stop
        
    # omni gate
    elseif findtype 27754 'ground' 2821 -1 2 as gate
        overhead "Omni Exit" 53
        dclick gate
        waitforgump 3773199800
        gumpresponse 2 3773199800

        if insysmsg 'too far away'
            continue
        endif
        
        stop
    endif
    
    if timer gtfoTimer > gtfoTimeout
        overhead 'Failed to GTFO!' 38
        stop
    endif
endwhile
